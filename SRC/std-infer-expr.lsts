
let std-infer-expr(tctx: Maybe<TContext>, term: AST, is-scoped: Bool, used: IsUsed, hint: Type): (TContext?, AST) = (
   match term {
      App{szof=left:Var{key:c"sizeof"}, right:AType{}} => (
         let tt = t1(c"U64");
         ascript-normal(term, tt);
         ascript-normal(szof, tt);
      );
      App{szof=left:Var{key:c"scope"}, r=right} => (
         infer-expr(tctx, r, Scoped, TAny, Tail);
         ascript-normal(term, typeof(r));
      );
      App{left:Lit{key:c":"}, right:App{t=left,right:AType{tt=tt}}} => (
         tt = tt.rewrite-type-alias;
         add-concrete-type-instance(tt);
         match t {
            Lit{} => ();
            _ => (tctx = infer-expr(tctx, t, Unscoped, tt, Tail));
         };
         ascript-normal(t, tt);
         ascript-normal(term, tt);
         if tt.is-t(c"SmartString") then maybe-apply-global-callable(c"intern", tt, term);
      );
      App{left:Var{key:c"as"}, right:App{t=left,right:AType{tt=tt}}} => (
         tt = tt.rewrite-type-alias;
         add-concrete-type-instance(tt);
         tctx = infer-expr(tctx, t, Unscoped, TAny, used);
         let inner-tt = typeof(t);
         if tt.tag.has-prefix(c"Tag::") then tt = tt && inner-tt
         else tt = tt && inner-tt.with-only-class;
         ascript-normal(term, tt);
      );
      App{left:App{ left:App{ left:Var{key:c"if"}, cond=right }, t=right }, f=right} => (
         if is-scoped {
            let tctx-inner = infer-expr(tctx, cond, Unscoped, TAny, Used);
            infer-expr(tctx-inner, t, Unscoped, TAny, Tail);
            infer-expr(tctx, f, Unscoped, TAny, Tail);
         } else {
            tctx = infer-expr(tctx, cond, Unscoped, TAny, Used);
            let tctx-t = infer-expr(tctx, t, Unscoped, TAny, Tail);
            infer-expr(tctx, f, Unscoped, TAny, Tail);
            tctx = tctx-t;
         };
         ascript-normal(term, typeof(t)); # TODO: use MGU to merge true and false branches
      );
      ASTEOF{} => ascript-normal(term, t1(c"Nil"));
      ASTNil{} => ascript-normal(term, t1(c"Nil"));
      Meta{} => ascript-normal(term, t1(c"Nil"));
      Typedef{} => ();
      AType{tt=tt} => ascript-normal(term, tt);
      Seq{left=left, right=right} => (
         (tctx, let new-left) = std-infer-expr(tctx, left, false, Used, TAny);
         (tctx, let new-right) = std-infer-expr(tctx, right, false, Used, TAny);
         if not(is(left,new-left)) || not(is(right,new-right))
         then term = mk-seq(new-left, new-right);
      );
      Glb{key=key, val=val} => (
         # TODO: remove after infer-expr.lm is ported
         #(tctx, let new-val) = std-infer-expr(tctx, val, false, Used);
         #if not(is(val,new-val)) then term = mk-glb(key, val);
         let rough-tt = typeof(term);
         if rough-tt.is-arrow && not(rough-tt.is-open) {
            infer-expr(tctx, val, if is-scoped then Scoped else Unscoped, TAny, used);
         };
      );
      Var{key=key, token=token} => (
         if key==c"__uninitialized" then ascript-normal(term, hint)
         else ascript-normal(term, typeof-var(term, tctx, key));
         mark-var-to-def-todo(tctx, key, TAny, term);
      );
      Lit{key=key, token=token} => (
         if not(non-zero(typeof(term))) {
            let tt = typeof-tag(key);
            if non-zero(tt) {
               if non-zero(hint) then tt = hint;
               ascript-normal(term,tt);
            }
         }
      );
      Abs{lhs=lhs, rhs=rhs:App{left:Lit{key:c":"},right:App{inner-rhs=left, right:AType{return-type=tt}}}, misc-tt=tt} => (
         tctx = infer-ctx(tctx, lhs);
         infer-expr(tctx, inner-rhs, Unscoped, return-type, Tail);
         ascript-normal(rhs, return-type);
         let domain-tt = typeof-lhs(lhs);
         ascript-normal(term, t3(c"Arrow", typeof-lhs(lhs), return-type) && misc-tt);
      );
      App{ is-cons=is-cons, l=left, r=right } => (
         if is-cons {
            tctx = infer-expr(tctx, l, Unscoped, TAny, Used);
            tctx = infer-expr(tctx, r, Unscoped, TAny, Used);
            ascript-normal(term, if is(used, Tail) then typeof(r) else t3(c"Cons", typeof(l), typeof(r)));
         } else {
            let rt = TAny;
            if var-name-if-var(l)==c"list::cons" { match r {
               App{k=left, m=right} => (
                  infer-expr(tctx, k, Unscoped, TAny, Call);
                  infer-expr(tctx, m, Unscoped, t2(c"List",normalize(typeof(k))), Used);
               );
               _ => ();
            }};
            if var-name-if-var(l)==c"map::cons" { match r {
               App{kv=left, m=right} => (
                  infer-expr(tctx, kv, Unscoped, TAny, Call);
                  infer-expr(tctx, m, Unscoped, t3(c"HashtableEq",normalize(typeof(kv)).r2,normalize(typeof(kv)).r1), Used);
               );
               _ => ();
            }};

            if index-macro-table.has(var-name-if-var(l)) {
               (tctx, term) = std-apply-macro(tctx, term);
            } else {
               tctx = infer-expr(tctx, l, Unscoped, TAny, used);
               if typeof(l).is-arrow {
                  tctx = infer-expr(tctx, r, Unscoped, TAny, Call);            
               } else {
                  tctx = infer-expr(tctx, r, Unscoped, TAny, Used);
               };
               rt = if typeof(l).is-arrow && non-zero(var-name-if-var-or-lit(l)) {
                  apply-global-callable(var-name-if-var-or-lit(l), typeof(r), term);
               } else {
                  t3(c"Cons", typeof(l), typeof(r))
               };
               if not(is(used,Call)) { rt = cons-tail-or-self(rt); };
               ascript-normal(term, rt);
            };
         };

         if not(index-macro-table.has(var-name-if-var(l))) && not(is-cons) && non-zero(var-name-if-var(l)) && typeof(l).is-arrow {
            mark-var-to-def-todo(tctx, var-name-if-var(l), typeof(r), l);
         };
      );
      _ => (
         # TODO: remove after infer-expr.lm is ported
         infer-expr(tctx, term, if is-scoped then Scoped else Unscoped, TAny, used);
      );
   };
   (tctx, term);
);

