
type ParsePartial (PME( AST , S )); # term, remainder
type SfxList SfxEOF | (SfxSeq( SfxList[] , String , Type )); zero SfxList SfxEOF;

parse-suffixes := (: SfxEOF SfxList);

parse-lambda := λ(: tokens S). (: (tail(
   (let expr ASTEOF)
   (let remainder SNil)
   (match (parse-one-expression tokens) (
      ()
      ( (PME( (Lit '._s) pr )) (match (parse-many-expressions pr) (
         ()
         ( (PME( le lr )) (tail(
            (set expr (Abs( (close ASTNil) (close le) TAny )))
            (set tokens SNil)
            (set remainder lr)
         )))
      )))
      ( (PME( pe pr )) (tail(
         (set expr pe)
         (set tokens pr)
      )))
   ))
   (while (non-zero tokens) (
      (match (parse-one-expression tokens) (
         ()
         ( (PME( (Lit '._s) r )) (
            (match (parse-many-expressions r) (
               ()
               ( (PME( le lr )) (tail(
                  (set expr (Abs( (close expr) (close le) TAny )))
                  (set remainder lr)
                  (set tokens SNil)
               )))
            ))
         ))
         ( (PME( le SNil )) (
            (parse-expect( 'Expected\sDot\sFor\sLambda\sBody\n_s tokens ))
         ))
         ( (PME( le lr )) (tail(
            (set expr (App( (close expr) (close le) )))
            (set tokens lr)
         )))
      ))
   ))
   (let pp ( (PME( expr remainder )) ))
   pp
)) ParsePartial);

parse-type-comma-sep := λ(: tt String). (: (tail(
   (let buff SNil)
   (let depth 0_u64)
   (let base TypeEOF)
   (while (head-string tt) (tail(
      (match (head-string tt) (
         ()
         ( 60_u8 (set depth (+( depth 1_u64 )) ))
         ( 62_u8 (set depth (-( depth 1_u64 )) ))
         ( 91_u8 (set depth (+( depth 1_u64 )) ))
         ( 93_u8 (set depth (-( depth 1_u64 )) ))
         ( _ () )
      ))
      (if (==( depth 0_u64 )) (
         (if (==( (head-string tt) 44_u8 )) (tail(
            (set base (TypeSeq(
               (close base)
               (parse-type(clone-rope buff))
            )))
            (set buff SNil)
         )) (
            (set buff (SCons(
               (close buff)
               (close (SAtom (clone-rope (head-string tt))))
            )))
         ))
      ) (
         (set buff (SCons(
            (close buff)
            (close (SAtom (clone-rope (head-string tt))))
         )))
      ))
      (set tt (tail-string tt))
   )))
   (set base (TypeSeq(
      (close base)
      (parse-type(clone-rope buff))
   )))
   base
)) TypeList);

parse-type := λ(: tt String). (: (tail(
   (let depth 0_u64)
   (let buff SNil)
   (let base-type (t1 'Nil_s))
   (while (head-string tt) (tail(
      (match (head-string tt) (
         ()
         ( 60_u8 (set depth (+( depth 1_u64 )) ))
         ( 62_u8 (set depth (-( depth 1_u64 )) ))
         ( 91_u8 (set depth (+( depth 1_u64 )) ))
         ( 93_u8 (set depth (-( depth 1_u64 )) ))
         ( _ () )
      ))
      (if (==( depth 0_u64 )) (
         (if (==( (head-string tt) 43_u8 )) (tail(
            (match base-type (
               ()
               ( (TGround( 'Nil_s TypeEOF )) (tail(
                  (let bt (parse-type-single (clone-rope buff)))
                  (set base-type bt)
               )))
               ( _ (
                  (set base-type (TAnd(
                     (close base-type)
                     (close(parse-type-single (clone-rope buff)))
                  )))
               ))
            ))
            (set buff SNil)
         )) (
            (set buff (SCons(
               (close buff)
               (close (SAtom (clone-rope (head-string tt))))
            )))
         ))
      ) (
         (set buff (SCons(
            (close buff)
            (close (SAtom (clone-rope (head-string tt))))
         )))
      ))
      (set tt (tail-string tt))
   )))
   (if (non-zero buff) (
      (match base-type (
         ()
         ( (TGround( 'Nil_s TypeEOF )) (tail(
            (let bt (parse-type-single (clone-rope buff)))
            (set base-type bt)
         )))
         ( _ (
            (set base-type (TAnd(
               (close base-type)
               (close(parse-type-single (clone-rope buff)))
            )))
         ))
      ))
   ) ())
   base-type
)) Type);

parse-many-expressions := λ(: tokens S). (: (tail(
   (let expr ASTEOF)
   (let remainder SNil)
   (while (non-zero tokens) (
      (match tokens (
         ()
         ( (LCons( '\:_t tl )) (tail(
            (set remainder tokens)
            (set tokens SNil)
         )))
         ( (LCons( '\]_t tl )) (tail(
            (if (non-zero expr) () (set expr ASTNil))
            (set remainder tl)
            (set tokens SNil)
         )))
         ( _ (
            (match (parse-one-expression tokens) (
               ()
               ( (PME( pe pr )) (tail(
                  (if (non-zero expr) (
                     (set expr (App(
                        (close expr)
                        (close pe)
                     )))
                  ) (
                     (set expr pe)
                  ))
                  (set tokens pr)
               )))
            ))
         ))
      ))
   ))
   (let pp ( (PME( expr remainder )) ))
   pp
)) ParsePartial);

parse-one-expression := λ(: tokens S). (: (tail(
   (let expr ASTEOF)
   (let remainder SNil)
   (match tokens (
      ()
      ( LEOF (tail(
         (set expr ASTNil)
         (set remainder SNil)
      )))
      ( (LCons( '\l_t r )) (
         (match (parse-lambda r) (
            ()
            ( (PME( le lr )) (tail(
               (set expr le)
               (set remainder lr)
            )))
         ))
      ))
      ( (LCons( '\]_t r )) (
         (parse-unexpect( 'Unexpected\sClosing\sParentheses_s tokens ))
      ))
      ( (LCons( '\[_t r )) (
         (match (parse-many-expressions r) (
            ()
            ( (PME( me mr )) (tail(
               (set expr me)
               (set remainder mr)
            )))
         ))
      ))
      ( (LCons( '\`_t
                (LCons( i_t r ))
        )) (tail(
         (set expr (Lit i))
         (set remainder r)
      )))
      ( (LCons( a_t r )) (tail(
         (if (is-variable a) (
            (set expr (Var a))
         ) (
            (set expr (Lit a))
         ))
         (set remainder r)
      )))
   ))
   (let pp ( (PME( expr remainder )) ))
   pp
)) ParsePartial);

parse-unexpect := λ(: msg String)(: tokens S). (: (tail(
   ()
   (fail msg)
)) Nil);
parse-expect := λ(: msg String)(: tokens S). (: (tail(
   ()
   (eprint msg)
   (print '\n_s)
   (print tokens)
   (exit 1_u64)
)) Nil);

parse-type-single := λ(: tt String). (: (tail(
   (let depth 0_u64)
   (let mode 0_u8)
   (let buff SNil)
   (let base-type (t1 'Nil_s))
   (while (head-string tt) (tail(
      (if (==( depth 0_u64 )) (
         (match (head-string tt) (
            ()
            ( 91_u8 (tail(
               (set mode 91_u8)
               (set depth 1_u64)
               (if (non-zero buff) (tail(
                  (let vn (clone-rope buff))
                  (if (==( vn '?_s )) (
                     (set base-type TAny)
                  ) (
                     (if (is-variable vn) (
                        (set base-type (TVar vn))
                     ) (
                        (set base-type (TGround(
                           (clone-rope buff)
                           (close TypeEOF)
                        )))
                     ))
                  ))
               )) ())
               (set buff SNil)
            )))
            ( 60_u8 (tail(
               (set mode 60_u8)
               (set depth 1_u64)
               (if (non-zero buff) (tail(
                  (let vn (clone-rope buff))
                  (if (==( vn '?_s )) (
                     (set base-type TAny)
                  ) (
                     (if (is-variable vn) (
                        (set base-type (TVar vn))
                     ) (
                        (set base-type (TGround(
                           (clone-rope buff)
                           (close TypeEOF)
                        )))
                     ))
                  ))
               )) ())
               (set buff SNil)
            )))
            ( c (
               (set buff (SCons(
                  (close buff)
                  (close (SAtom (clone-rope c)))
               )))
            ))
         ))
      ) (tail(
         (match (head-string tt) (
            ()
            ( 60_u8 (set depth (+( depth 1_u64 )) ))
            ( 62_u8 (set depth (-( depth 1_u64 )) ))
            ( 91_u8 (set depth (+( depth 1_u64 )) ))
            ( 93_u8 (set depth (-( depth 1_u64 )) ))
            ( _ () )
         ))
         (if (==( depth 0_u64 )) (
            (match mode (
               ()
               ( 91_u8 (
                  (if (non-zero buff) (tail(
                     (let base-type-2 (t3(
                        'Array_s
                        base-type
                        (parse-type(clone-rope buff))
                     )))
                     (set base-type base-type-2)
                     (set buff SNil)
                  )) (tail(
                     (let base-type-2 (t3(
                        'Array_s
                        base-type
                        TAny
                     )))
                     (set base-type base-type-2)
                  )))
               ))
               ( 60_u8 (match base-type (
                  ()
                  ( (TGround( tag TypeEOF )) (tail(
                     (set base-type (TGround(
                        tag
                        (close(parse-type-comma-sep(clone-rope buff)))
                     )))
                     (set buff SNil)
                  )))
                  ( (TVar( tag )) (tail(
                     (set base-type (TGround(
                        tag
                        (close(parse-type-comma-sep(clone-rope buff)))
                     )))
                     (set buff SNil)
                  )))
                  ( _ (tail(
                     (eprint 'Unexpected\sParameterized\sType:\s_s)
                     (print base-type)
                     (exit 1_u64)
                  )))
               )))
            ))
         ) (
            (set buff (SCons(
               (close buff)
               (close (SAtom (clone-rope (head-string tt))))
            )))
         ))
      )))
      (set tt (tail-string tt))
   )))
   (if (non-zero buff) (tail(
      (let vn (clone-rope buff))
      (if (==( vn '?_s )) (
         (set base-type TAny)
      ) (
         (if (is-variable vn) (
            (set base-type (TVar vn))
         ) (
            (set base-type (TGround(
               (clone-rope buff)
               (close TypeEOF)
            )))
         ))
      ))
   )) ())
   base-type
)) Type);

parse := λ. (: (tail(
   (let tokens ast-tokenized-program)
   (set ast-tokenized-program (: LEOF List<Token>))
   (while (non-zero tokens) (
      (match tokens (
         ()
         ( (LCons( '\:_t remainder )) (
            (set tokens remainder)
         ))
         ( (LCons( 'macro_t remainder )) (
            (match (parse-one-expression remainder) (
               ()
               ( (PME( mlhs mremainder1 )) (
                  (match (parse-one-expression mremainder1) (
                     ()
                     ( (PME( mrhs mremainder2 )) (tail(
                        (set preprocess-macros (MSeq(
                           (close preprocess-macros)
                           (Macro( mlhs mrhs ))
                        )))
                        (set tokens mremainder2)
                     )))
                  ))
               ))
            ))
         ))
         ( (LCons( 'fragment_t (LCons( 'type_t remainder )) )) (
            (match (parse-one-expression remainder) (
               ()
               ( (PME( e1 r1 )) (match (parse-many-expressions r1) (
                  ()
                  ( (PME( e2 r2 )) (tail(
                     (set ast-parsed-program (Seq(
                        (close ast-parsed-program)
                        (close (Typedef(
                           (close e1)
                           (close e2)
                        )))
                     )))
                     (let base-type (type-of-s e1))
                     (index-tag-is-fragment(tag-of base-type))
                     (set tokens r2)
                  )))
               )))
            ))
         ))
         ( (LCons( 'fragment_t remainder-ext )) (tail(
            (let tt-ext TAny)
            (match remainder-ext (
               ()
               ( (LCons( ':_t (LCons( tt-lit_t rr )) )) (tail(
                  (set tt-ext (parse-type tt-lit))
                  (set remainder-ext rr)
               )))
               ( _ () )
            ))
            (match remainder-ext (
               ()
               ( (LCons( key_t (LCons( ':=_t remainder )) )) (
                  (match (parse-many-expressions remainder) (
                     ()
                     ( (PME( re rr )) (tail(
                        (set ast-parsed-program (Seq(
                           (close ast-parsed-program)
                           (close(Frg( key (close re) tt-ext )))
                        )))
                        (set tokens rr)
                     )))
                  ))
               ))
            ))
         )))
         ( (LCons( 'type_t remainder )) (
            (match (parse-one-expression remainder) (
               ()
               ( (PME( e1 r1 )) (match (parse-many-expressions r1) (
                  ()
                  ( (PME( e2 r2 )) (tail(
                     (set ast-parsed-program (Seq(
                        (close ast-parsed-program)
                        (close (Typedef(
                           (close e1)
                           (close e2)
                        )))
                     )))
                     (set tokens r2)
                  )))
               )))
            ))
         ))
         ( (LCons( 'atom_t (LCons( 'suffix_t
                   (LCons( atype_t (LCons( suffix_t remainder )) ))
           )) )) (tail(
            (set parse-suffixes (SfxSeq(
               (close parse-suffixes)
               suffix
               (TAnd(
                  (close(TAnd(
                     (close(TGround( 'Constant_s (close TypeEOF) )))
                     (close(TGround( 'Literal_s (close TypeEOF) )))
                  )))
                  (close(parse-type atype))
               ))
            )))
            ()
            (set tokens remainder)
         )))
         ( (LCons( 'size_t (LCons( class_t (LCons( sz_t remainder )) )) )) (tail(
            (index-size-of-class( class (to-u64 sz) ))
            (set tokens remainder)
         )))
         ( (LCons( 'import_t (LCons( relative-path_t remainder )) )) (tail(
            (tokenize relative-path)
            (parse ())
            (set tokens remainder)
         )))
         ( (LCons( 'zero_t (LCons( base-type_t (LCons( tag-type_t remainder )) )) )) (tail(
            (set ast-parsed-program (Seq(
               (close ast-parsed-program)
               (close (Glb( 'non-zero_s (close(Abs(
                  (close(App(
                     (close(Lit(clone-rope(SAtom ':_s))))
                     (close(App( (close(Var(clone-rope(SAtom 't_s)))) (close(AType(parse-type base-type))) )))
                  )))
                  (close(App(
                     (close(Lit(clone-rope(SAtom ':_s))))
                     (close(App(
                        (close(App(
                           (close(Var(clone-rope(SAtom '!=_s))))
                           (close(App(
                              (close(App(
                                 (close(Var(clone-rope(SAtom '.0_s))))
                                 (close(Var(clone-rope(SAtom 't_s))))
                              )))
                              (close(App(
                                 (close(Var(clone-rope(SAtom '.0_s))))
                                 (close(App(
                                    (close(Lit ':_s))
                                    (close(App(
                                       (close(Lit tag-type))
                                       (close(AType(parse-type base-type)))
                                    )))
                                 )))
                              )))
                           )))
                        )))
                        (close(AType(t1 'U64_s)))
                     )))
                  )))
                  TAny
               ))) )))
            )))
            (set tokens remainder)
         )))
         ( (LCons( key_t (LCons( ':=_t remainder )) )) (
            (match (parse-many-expressions remainder) (
               ()
               ( (PME( re rr )) (tail(
                  (set ast-parsed-program (Seq(
                     (close ast-parsed-program)
                     (close (Glb( key (close re) )))
                  )))
                  (set tokens rr)
               )))
            ))
         ))
         (remainder (
            (match (parse-many-expressions remainder) (
               ()
               ( (PME( term remainder )) (tail(
                  (set ast-parsed-program (Seq(
                     (close ast-parsed-program)
                     (close term)
                  )))
                  (set tokens remainder)
               )))
            ))
         ))
      ))
   ))
)) Nil);
