
let apply-blame(function-name: CString, function-type: Type, parameters: Type, do-specialize: U64, blame: AST): ApplyResult = (
   let r = apply(function-name, function-type, parameters, do-specialize);
   if not(non-zero(r.return-type)) {
      fail("Function Application Yielded No Matches\n"
           "\{function-name}\n"
           "With Arguments \{parameters}\n"
           "At \{blame.location}\n"
           "With Candidates:\n"
           "\{function-type.pretty}\n")
   };
   if r.return-type.is-t(c"Error: Did Not Close Before Specialization") {
      fail("Application\sDid\sNot\sClose\sBefore\sSpecialization:\n"
           "Function \{function-name}: \{function-type}\n"
           "Argument \{parameters}\n"
           "At \{blame.location}\n");
   };
   if r.return-type.is-t(c"Error: Function Application Yielded An Irreducible Plurality Of Matches") {
      fail("Function Application Yielded An Irreducible Plurality Of Matches\n"
           "\{function-name}\n"
           "With Arguments \{parameters}\n"
           "At \{blame.location}\n"
           "Matched Candidates:\n"
           "\{r.function-type.pretty}\n")
   };
   r
);

let apply(function-name: CString, function-type: Type, parameters: Type, do-specialize: U64): ApplyResult = (
   let r = TAny {};
   let rs = apply-plural(function-type, parameters);
   if not(function-type.is-t( c"Hook" )) {
      rs = reduce-plural(rs);
   };
   if rs.length > 1 {
      r = t1(c"Error: Function Application Yielded An Irreducible Plurality Of Matches");
      function-type = head(rs);
      for candidate in tail(rs) {
         function-type = function-type && candidate;
      };
   } else {
      for sft in rs { # iterate in case this is a hook
         let frt = sft.range;
         let fpt = sft.domain;
         let ctx = unify(fpt, parameters);
         let closed-type = substitute( ctx, sft );
         function-type = closed-type;
         if not(r.is-t( c"Error: Did Not Close Before Specialization")) {
            r = substitute(ctx, frt);
         };
         if do-specialize && sft.is-open {
            if closed-type.is-open {
               r = t1(c"Error: Did Not Close Before Specialization");
            };
            try-specialize(function-name, sft, ctx, closed-type);
         }
      };
      if function-type.is-t(c"Prop") {
         r = r && cons-root(parameters);
      };
   };
   ApplyResult { function-type, r }
);

