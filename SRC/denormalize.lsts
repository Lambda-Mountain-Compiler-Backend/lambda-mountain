
let denormalize-cache = mk-hashtable-is(type(Type), type(Type));

let denormalize(tt: Type): Type = (
   let ct = denormalize-cache.lookup(tt, TAny);
   if non-zero(ct) then ct else (
      ct = tt.rewrite-type-alias;
      ct = with-size(ct);
      ct = with-tag(ct);
      ct = enrich-quick-prop(ct);
      denormalize-cache = denormalize-cache.bind(tt, ct);
      ct
   )
);

let denormalize-arrow(pt: Type): Type = (
   match pt {
      TAnd{ conjugate=conjugate } => (
         let result = TAny;
         for vector c in conjugate { result = result && denormalize-arrow(c) };
         result
      );
      TGround{tag:c"Arrow", parameters:[rng.. dom..]} => (
         t3( c"Arrow", denormalize-cons(dom), rng )
      );
      TGround{tag:c"Array", parameters:[rng.. dom..]} => (
         t3( c"Array", denormalize-arrow(dom), rng )
      );
      TGround{} => denormalize(pt);
      _ => pt;
   }
);

let denormalize-cons(pt: Type): Type = (
   match pt {
      TAnd{ conjugate=conjugate } => (
         let result = TAny;
         for vector c in conjugate { result = result && denormalize-cons(c) };
         result
      );
      TGround{tag:c"Cons", parameters:[rng.. dom..]} => (
         t3( c"Cons", denormalize-cons(dom), denormalize-cons(rng) )
      );
      TGround{} => denormalize(pt);
      _ => pt;
   }
);
