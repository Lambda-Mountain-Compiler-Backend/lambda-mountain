
blob-expr := λ(: ctx FContext)(: term AST). (: (
   (let e (fragment::new()))
   (set e (fragment::set-context( e ctx )))
   (match term (
      ()
      ( ASTNil () )
      ( ASTEOF () )
      ( (Var( id _ )) (
         (set e (fragment-context::lookup( ctx id (typeof term) term )))
      ))
      ( (Lit( val _ )) (
         (set e (fragment::set( e 'program_s (SAtom val) )))
      ))
      ( (App( (Abs( (Var( lhs _ )) ASTNil tlt )) rhs )) (
         (let rtype (typeof rhs))
         (set ctx (fragment-context::bind(
            ctx lhs rtype (blob-expr( ctx rhs ))
         )))
         (set e (fragment::set-context( e ctx )))
      ))
      ( (App( f a )) (
         (match (slot( (typeof f) 'Arrow_s )) (
            ()
            ( (TGround( 'Arrow_s _ )) (
               (match f (
                  ()
                  ( (Var( fname _ )) (
                     (set e (blob-call( ctx fname a )))
                  ))
               ))
            ))
            ( _ (
               (let e1 (blob-expr( ctx f )))
               (let e2 (blob-expr( ctx a )))
               (set e (fragment::chain( e1 e2 )))
               (set e (fragment::set-type( e (typeof term) )))
            ))
         ))
      ))
   ))
   e
) Fragment);

blob-call := λ(: ctx FContext)(: function-name String)(: args AST). (: (
   (let f (fragment-context::lookup( ctx function-name (typeof args) args )))
   (let f-args (blob-args( ctx args )))
   (let r (fragment-apply( ctx 0_i64 function-name f-args (range(fragment::get-type f)) args )))
   (set r (fragment::set-context( r ctx )))
   r
) Fragment);

blob-args := λ(: ctx FContext)(: rval AST). (: (
   (let r FLEOF)
   (match (slot( (typeof rval) 'Cons_s )) (
      ()
      ( (TGround( 'Cons_s (TypeSeq( (TypeSeq( TypeEOF p1 )) p2 )) )) (
         (match rval (
            ()
            ( (App( le re )) (
               (let e1 (blob-args( ctx le )))
               (let e2 (blob-expr( ctx re )))
               (set r (FLSeq( (close e1) e2 )))
            ))
         ))
      ))
      ( _ (
         (let e1 (blob-expr( ctx rval )))
         (set r (FLSeq( (close FLEOF) e1 )))
      ))
   ))
   r
) FragmentList);
