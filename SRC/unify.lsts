
let unify(fpt: Type, pt: Type): Maybe<TContext> = (
   let ctx = None :: Maybe<TContext>;
   if can-unify(fpt, pt) {
      ctx = unify-inner(fpt, pt);
   };
   ctx
);

let unify-inner(fpt: Type, pt: Type): Maybe<TContext> = (
   let ctx = None :: Maybe<TContext>;
   let yes = Some([] :: TContext);
   match (Tuple{fpt, pt}) {
      Tuple{ first:TAny{} } => ctx = yes;
      Tuple{ first:TGround{tag:c"Any"} } => ctx = yes;
      Tuple{ first:TVar{name=name}, second:TGround{tag=tag} } => (
         ctx = ctx.bind(name, normalize(pt), Lit{tag, mk-token(tag)});
      );
      Tuple{ first:TVar{name=name} } => ctx = ctx.bind(name, normalize(pt), ASTEOF);
      Tuple{ first:TAnd{ lconjugate=conjugate }, rt=second } => (
         ctx = yes;
         for vector c in lconjugate { ctx = ctx && unify-inner(c,rt) };
         ctx
      );
      Tuple{ lt=first, second:TAnd{ rconjugate=conjugate } } => (
         ctx = yes;
         for vector c in rconjugate { ctx = union(ctx, unify-inner(lt,c)) };
         ctx
      );

      # Ignore C Access Modifiers For Now
      Tuple{ first:TGround{tag:c"C", parameters:[TGround{tag:c"const"}..]} } => yes;

      # Literal Constants
      Tuple{ first:TGround{tag:c"CONST"}, second:TGround{c=tag} } => (
         if c.is-digit || c==c"CONST" { ctx = yes; }
      );

      # Varargs
      Tuple{
          first:TGround{tag:c"Cons", parameters:[
             TGround{tag:c"...", parameters:[lp1..]}..
             lpr..
          ]},
          second:TGround{tag:c"Cons", parameters:[rp1..rpr..]}
      } => (
         ctx = unify-inner(lpr, pt);
      );
      Tuple{
          first:TGround{tag:c"Cons", parameters:[
             TGround{tag:c"...", parameters:[lp1..]}..
             lpr..
          ]},
          second:rp1
      } => (
         if can-unify(lp1, rp1)
         then { ctx = unify-inner(lp1,rp1) && unify-inner(lpr, t1(c"Nil")); }
         else { ctx = unify-inner(lpr,rp1); }
      );
      Tuple{
          first:TGround{tag:c"...", parameters:[lp1..]},
          second:TGround{tag:c"Cons", parameters:[rp1..rpr..]}
      } => (
         if can-unify(lp1,rp1) { ctx = unify-inner(fpt,rpr) && unify-inner(lp1,rp1); };
      );
      Tuple{
          first:TGround{tag:c"...", parameters:[lp1..]},
          second:TGround{tag:c"Nil", parameters:[]}
      } => ctx = yes;
      Tuple{
          first:TGround{tag:c"...", parameters:[lp1..]},
          second:rp1
      } => ctx = unify-inner(lp1,rp1);

      Tuple{
          first:TGround{tag:c"Arrow", parameters:[lrng.. ldom..]},
          second:TGround{tag:c"Arrow", parameters:[rrng.. rdom..]}
      } => (
          let tctx = unify-inner(rdom, ldom);
          if tctx.is-some {
            if rdom.is-open {
               ctx = tctx && unify-inner(lrng, substitute(tctx,rrng));
            } else {
               ctx = tctx && unify-inner(lrng, rrng)
            }
         }
      );

      Tuple{
          first:TGround{ltn=tag, lps=parameters},
          second:TGround{rtn=tag, rps=parameters}
      } => if ltn==rtn then { ctx = unify(lps,rps) };
      _ => ();
   };
   ctx
);

let unify(fpt: List<Type>, pt: List<Type>): Maybe<TContext> = (
   let ctx = None :: Maybe<TContext>;
   let yes = Some([] :: TContext);
   match Tuple{ fpt, pt } {
      Tuple{ first:[lp1..lps], second:[rp1..rps] } => (
         ctx = unify-inner(lp1,rp1);
         if non-zero(ctx) { ctx = union(ctx, unify(lps,rps)); }
      );
      Tuple{ first:[], second:[] } => ctx = yes;
      Tuple{} => ();
   };
   ctx
);

