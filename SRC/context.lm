
type CCContext (CCCEOF) | (CCCtxBind( CCContext[] , String , Type , CCFragment )); zero CCContext CCCEOF;

print := λ(: ctx CCContext). (: (tail(
   (print 'Context:\n_s)
   (while (non-zero ctx) (match ctx (
      ()
      ( (CCCtxBind( rst k kt kf )) (tail(
         (print k)(print '\s:\s_s)(print kt)(print '\n_s)(print kf)
         (set ctx rst)
      )))
   )))
)) Nil);

close := λ(: x CCContext). (: (tail(
   (mov( (malloc(sizeof CCContext)) R8 ))
   (mov( x 0_u64 (as R8 CCContext[]) ))
   (as R8 CCContext[])
)) CCContext[]);

cccontext::new := λ. (: (tail(
   (let r CCCEOF)
   r
)) CCContext);

cccontext::lookup := λ(: ctx CCContext)(: k String)(: kt Type)(: sloc AST). (: (tail(
   (let r (maybe-deref(ccfragment::new())))
   (let found False_u8)
   (while (non-zero ctx) (match ctx (
      ()
      ( (CCCtxBind( rst rk rt rf )) (
         (if (==( k rk )) (
            (match (maybe-deref(typecheck-slot( rt '->_s ))) (
               ()
               ( (TGround( '->_s _ )) (tail(
                  (let u (maybe-deref(typecheck-apply-plural( sloc rt kt ))))
                  (if (non-zero u) (tail(
                     (set r rf)
                     (set found True_u8)
                     (set ctx CCCEOF)
                  )) (set ctx rst))
               )))
               ( _ (tail(
                  (set r rf)
                  (set found True_u8)
                  (set ctx CCCEOF)
               )))
            ))
         ) (
            (set ctx rst)
         ))
      ))
   )))
   (if (==( found True_u8 )) () (tail(
      (print 'Context::lookup\s_s)(print k)(print '\s:\s_s)(print kt)(print '\n_s)
      (exit-error( 'Context::lookup\sCould\sNot\sFind\sSymbol_s sloc ))
   )))
   r
)) CCFragment);

cccontext::bind := λ(: ctx CCContext)(: k String)(: kt Type)(: f CCFragment). (: (tail(
   (let new-ctx (CCCtxBind(
      (close ctx)
      k kt f
   )))
   new-ctx
)) CCContext);

