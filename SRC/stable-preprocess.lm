
index-typedefs := λ(: program AST). (: (
   (while (non-zero program) (match program (
      ()
      ( (Seq( rst (Typedef( lhs rhs )) )) (tail(
         (index-typedefs( rhs 0_u64 ))
         (set program rst)
      )))
      ( (Seq( rst _ )) (
         (set program rst)
      ))
      ( _ (set program ASTEOF) )
   )))
) Nil);

index-typedefs := λ(: def AST)(: index U64) . (: (
   (match def (
      ()
      ( (App( (App( tds (Var '|_s) )) (Lit tag) )) (tail(
         (index-index-of-tag( tag index ))
         (index-typedefs( tds (+( index 1_u64 )) ))
      )))
      ( (App( (App( tds (Var '|_s) )) (App( (Lit tag) _ )) )) (tail(
         (index-index-of-tag( tag index ))
         (index-typedefs( tds (+( index 1_u64 )) ))
      )))
      ( (Lit tag) (
         (index-index-of-tag( tag index ))
      ))
      ( (App( (Lit tag) _ )) (
         (index-index-of-tag( tag index ))
      ))
      ( ASTEOF () )
      ( _ (exit-error( 'Invalid\sTypedef_s def )) )
   ))
) Nil);

preprocess := λ. (: (tail(
   (index-typedefs ast-parsed-program)
   (set ast-parsed-program (preprocess-apply-literals ast-parsed-program))
   (set ast-parsed-program (preprocess-apply ast-parsed-program))
)) Nil);

union := λ(: l Context)(: r Context). (: (tail(
   (let return l)
   (if (not(non-zero r)) (set return CtxEOF) ())
   (if (non-zero return) (
      (match r (
         ()
         ( (CtxBind( rst k v )) (
            (set return (CtxBind(
               (close(union( l rst ))) k v
            )))
         ))
         ( _ (set r CtxEOF) )
      ))
   ) ())
   return
)) Context);

is-macro-head := λ(: s String)(: arity U64). (: (tail(
   (let macros preprocess-macros)
   (let found 0_u64)
   (while (non-zero macros) (match macros (
      ()
      ( (MSeq( rst (Macro( (App( (Lit mv) p1 )) mrhs )) )) (tail(
         (if (&&( (==( arity 1_u64 )) (==( s mv )) )) (
            (set found 1_u64)
         ) ())
         (set macros rst)
      )))
      ( (MSeq( rst (Macro( (App( (App( (Lit mv) p1 )) p2 )) mrhs )) )) (tail(
         (if (&&( (==( arity 2_u64 )) (==( s mv )) )) (
            (set found 1_u64)
         ) ())
         (set macros rst)
      )))
      ( (MSeq( rst (Macro( mlhs mrhs )) )) (
         (exit-error( 'Unrecognized\sMacro\sPattern\n_s mlhs ))
      ))
   )))
   found
)) U64);

preprocess-apply-literals := λ(: program AST). (: (tail(
   (let r program)
   (match program (
      ()
      ( (Lit l) (
         (if (has-suffix( l '_t_s )) (tail(
            (let lloc (location-of l))
            (let lpfx (remove-suffix( l '_t_s )))
            (set r (App(
               (close(Lit(unique 'Token_s)))
               (close(App(
                  (close(App(
                     (close(App(
                        (close(Lit(clone-rope(SAtom ':_s))))
                        (close(App( (close(Lit(unique lpfx))) (close(AType(parse-type 'Literal+Constant+String_s))) )))
                     )))
                     (close(Var(unique '__s)))
                  )))
                  (close(Var(unique '__s)))
               )))
            )))
         )) ())
      ))
      ( (Var l) (
         (if (has-suffix( l '_t_s )) (tail(
            (let lloc (location-of l))
            (let lpfx (remove-suffix( l '_t_s )))
            (set r (App(
               (close(Lit(unique 'Token_s)))
               (close(App(
                  (close(App(
                     (close(Var(unique lpfx)))
                     (close(Var(unique '__s)))
                  )))
                  (close(Var(unique '__s)))
               )))
            )))
         )) ())
      ))
      ( (App( t1 t2 )) (
         (set r (App(
            (close(preprocess-apply-literals t1))
            (close(preprocess-apply-literals t2))
         )))
      ))
      ( (Seq( al ar )) (set r (Seq(
         (close(preprocess-apply-literals al))
         (close(preprocess-apply-literals ar))
      ))))
      ( (App( al ar )) (set r (App(
         (close(preprocess-apply-literals al))
         (close(preprocess-apply-literals ar))
      ))))
      ( (Abs( al ar tlt )) (set r (Abs(
         (close(preprocess-apply-literals al))
         (close(preprocess-apply-literals ar))
         tlt
      ))))
      ( (Frg( al ar atlt )) (
         (set r (Frg(
            al
            (close(preprocess-apply-literals ar))
            atlt
         )))
      ))
      ( (Glb( k ar )) (set r (Glb(
         k
         (close(preprocess-apply-literals ar))
      ))))
      ( u (set r u))
   ))
   r
)) AST);

preprocess-apply := λ(: program AST). (: (tail(
   (let r program)
   (match program (
      ()
      ( (App( (App( (Lit ':_s) (App( t (AType tt) )) )) nt )) (
         (set r (App(
            (close(App( (close(Lit(clone-rope(SAtom ':_s)))) (close(App( (close(preprocess-apply t)) (close(AType tt)) ))) )))
            (close(preprocess-apply( nt )))
         )))
      ))
      ( (App( (App( (Lit ':_s) mvar )) (Lit mtype) )) (
         (set r (App( (close(Lit(clone-rope(SAtom ':_s)))) (close(App( (close(preprocess-apply mvar)) (close(AType(parse-type mtype))) ))) )))
      ))
      ( (App( (App( (Lit ':_s) mvar )) (Var mtype) )) (
         (set r (App( (close(Lit(clone-rope(SAtom ':_s)))) (close(App( (close(preprocess-apply mvar)) (close(AType(parse-type mtype))) ))) )))
      ))
      ( (App( (App( (Var 'as_s) (App( t (AType tt) )) )) nt )) (
         (set r (App(
            (close(App( (close(Var(clone-rope(SAtom 'as_s)))) (close(App( (close(preprocess-apply t)) (close(AType tt)) ))) )))
            (close(preprocess-apply( nt )))
         )))
      ))
      ( (App( (App( (Var 'as_s) mvar )) (Lit mtype) )) (
         (set r (App( (close(Var(clone-rope(SAtom 'as_s)))) (close(App( (close(preprocess-apply mvar)) (close(AType(parse-type mtype))) ))) )))
      ))
      ( (App( (App( (Var 'as_s) mvar )) (Var mtype) )) (
         (set r (App( (close(Var(clone-rope(SAtom 'as_s)))) (close(App( (close(preprocess-apply mvar)) (close(AType(parse-type mtype))) ))) )))
      ))
      ( (App( (Var 'sizeof_s) (Var mtype) )) (
         (set r (App( (close(Var(clone-rope(SAtom 'sizeof_s)))) (close(AType(parse-type mtype))) )))
      ))
      ( (App( (Var 'sizeof_s) (Lit mtype) )) (
         (set r (App( (close(Var(clone-rope(SAtom 'sizeof_s)))) (close(AType(parse-type mtype))) )))
      ))
      ( (Lit l) (tail(
         (let suffixes parse-suffixes)
         (while (non-zero suffixes) (match suffixes (
            ()
            ( (SfxSeq( rst sfxs sfxtt )) (
               (if (has-suffix( l sfxs )) (tail(
                  (let lloc (location-of l))
                  (let lpfx (remove-suffix( l sfxs )))
                  (map-location( lpfx lloc ))
                  (set r (App(
                     (close(Lit(clone-rope(SAtom ':_s))))
                     (close(App( (close (Lit lpfx)) (close(AType sfxtt)) )))
                  )))
                  (set suffixes SfxEOF)
               )) (
                  (set suffixes rst)
               ))
            ))
         )))
      )))
      ( (Var l) (tail(
         (let suffixes parse-suffixes)
         (while (non-zero suffixes) (match suffixes (
            ()
            ( (SfxSeq( rst sfxs sfxtt )) (
               (if (has-suffix( l sfxs )) (tail(
                  (let lloc (location-of l))
                  (let lpfx (remove-suffix( l sfxs )))
                  (map-location( lpfx lloc ))
                  (set r (App(
                     (close(Lit(clone-rope(SAtom ':_s))))
                     (close(App( (close (Lit lpfx)) (close(AType sfxtt)) )))
                  )))
                  (set suffixes SfxEOF)
               )) (
                  (set suffixes rst)
               ))
            ))
         )))
      )))
      ( (App( (Var vn) vt )) (
         if (is-macro-head( vn 1_u64 )) (tail(
            (let applied (preprocess-apply-hard( vn 1_u64 program )))
            (set r applied)
         )) (
            (set r (App(
               (close(preprocess-apply(Var vn)))
               (close(preprocess-apply vt))
            )))
         )
      ))
      ( (App( (App( (Var vn) vt1 )) vt2 )) (
         if (is-macro-head( vn 2_u64 )) (tail(
            (let applied (preprocess-apply-hard( vn 2_u64 program )))
            (set r applied)
         )) (
            (set r (App(
               (close(preprocess-apply(App(
                  (close(Var vn))
                  (close vt1)
               ))))
               (close(preprocess-apply vt2))
            )))
         )
      ))
      ( (Seq( al ar )) (set r (Seq(
         (close(preprocess-apply al))
         (close(preprocess-apply ar))
      ))))
      ( (App( al ar )) (set r (App(
         (close(preprocess-apply al))
         (close(preprocess-apply ar))
      ))))
      ( (Abs( al ar tlt )) (set r (Abs(
         (close(preprocess-apply al))
         (close(preprocess-apply ar))
         tlt
      ))))
      ( (Frg( al ar atlt )) (
         (set r (Frg(
            al
            (close(preprocess-apply ar))
            atlt
         )))
      ))
      ( (Glb( k ar )) (set r (Glb(
         k
         (close(preprocess-apply ar))
      ))))
      ( u (set r u))
   ))
   r
)) AST);

preprocess-apply-hard := λ(: macro-name String)(: arity U64)(: program AST). (: (tail(
   (let return program)
   (let macros preprocess-macros)
   (let matched False_u8)
   (while (non-zero macros) (match macros (
      ()
      ( (MSeq( rst (Macro( lhs rhs )) )) (tail(
         (set macros rst)
         (let go False_u8)
         (match lhs (
            ()
            ( (App( (Lit m) _ )) (
               (if (&&( (==( macro-name m)) (==( arity 1_u64 )) )) (
                  (set go True_u8)
               ) ())
            ))
            ( (App( (App( (Lit m) _ )) _ )) (
               (if (&&( (==( macro-name m)) (==( arity 2_u64 )) )) (
                  (set go True_u8)
               ) ())
            ))
         ))
         (if (==( go True_u8 )) (
            (match (try-destructure-macro( (location-of program) lhs program )) (
               ()
               ( CtxEOF (set macros rst) )
               ( ctx (tail(
                  (let p (apply( ctx rhs )))
                  (let c (extract-uuids( (location-of program) CtxEOF p )))
                  (if (non-zero c) (
                     (set p (substitute-uuids( c p )))
                  ) ())
                  (set return (preprocess-apply p))
                  (set return (relocate( return (location-of program) )))
                  (set macros MEOF)
                  (set matched True_u8)
               )))
            ))
         ) ())
      )))
      ( _ (set macros MEOF) )
   )))
   (if (not matched) (
      (exit-error( 'Invalid\sMacro\sApplication\n_s program ))
   ) ())
   return
)) AST);

apply := λ(: ctx Context)(: term AST). (: (tail(
   (let return term)
   (match term (
      ()
      ( ASTEOF () )
      ( ASTNil () )
      ( (Lit _) () )
      ( (AType _) () )
      ( (Var n) (
         (while (non-zero ctx) (match ctx (
            ()
            ( (CtxBind( rst k v )) (
               (if (==( k n )) (tail(
                  (set return v)
                  (set ctx CtxEOF)
               )) (
                  (set ctx rst)
               ))
            ))
            ( _ (set ctx CtxEOF))
         )))
      ))
      ( (App( vl vr )) (
         (set return (App(
            (close(apply( ctx vl )))
            (close(apply( ctx vr )))
         )))
      ))
      ( (Abs( vl vr tlt )) (
         (set return (Abs(
            (close(apply( ctx vl )))
            (close(apply( ctx vr )))
            tlt
         )))
      ))
      ( (Seq( vl vr )) (
         (set return (Seq(
            (close(apply( ctx vl )))
            (close(apply( ctx vr )))
         )))
      ))
      ( (Typedef( vl vr )) (
         (set return (Typedef(
            (close(apply( ctx vl )))
            (close(apply( ctx vr )))
         )))
      ))
      ( (Glb( k vr )) (
         (set return (Glb(
            k
            (close(apply( ctx vr )))
         )))
      ))
      ( (Frg( k vr tlt )) (
         (set return (Frg(
            k
            (close(apply( ctx vr )))
            tlt
         )))
      ))
   ))
   return
)) AST);

try-destructure-macro := λ(: loc SourceLocation)(: lhs AST)(: term AST). (: (tail(
   (let r CtxEOF)
   (match (Pair( lhs term )) (
      ()
      ( (Pair( ASTNil ASTNil )) (set r CtxNil) )
      ( (Pair( (App( (App( (Var _) (Var _) )) (Var _) )) (App( (App( (Lit _) _ )) _ )) )) () )
      ( (Pair( (App( (Lit ':Literal:_s) (Var pv) )) (App( (Lit ':_s) (App( (Lit _) (AType _) )) )) )) (
         (set r (CtxBind( (close CtxNil) pv (relocate( term loc )) )))
      ))
      ( (Pair( (App(pl pr)) (App(el er)) )) (tail(
         (let ll (try-destructure-macro( loc pl el )))
         (if (non-zero ll) (tail(
            (let rl (try-destructure-macro( loc pr er )))
            (if (non-zero rl) (
               (set r (union( ll rl )))
            ) ())
         )) ())
      )))
      ( (Pair( (Abs(pl pr ptlt)) (Abs(el er etlt)) )) (tail(
         (let ll (try-destructure-macro( loc pl el )))
         (if (non-zero ll) (tail(
            (let rl (try-destructure-macro( loc pr er )))
            (if (non-zero rl) (
               (set r (union( ll rl )))
            ) ())
         )) ())
      )))
      ( (Pair( (Lit ':Any:_s) (Var '__s) )) (
         (set r CtxNil)
      ))
      ( (Pair( (Lit pl) (Var el) )) (
         (if (==( pl el )) (set r CtxNil) ())
      ))
      ( (Pair( (Lit pl) (Lit el) )) (
         (if (==( pl el )) (set r CtxNil) ())
      ))
      ( (Pair( (App( (Lit ':Literal:_s) (Var pv) )) (Lit el) )) (
         (if (==( (index-of-tag el) unknown-index-of-tag ))
             (set r (CtxBind( (close CtxNil) pv (relocate( term loc )) )))
             ()
         )
      ))
      ( (Pair( (App( (Lit ':Variable:_s) _ )) (Var '__s) )) (
         ()
      ))
      ( (Pair( (App( (Lit ':Variable:_s) (Var pv) )) (Var el) )) (
         (set r (CtxBind( (close CtxNil) pv (relocate( term loc )) )))
      ))
      ( (Pair( (App( (App( (Lit ':Tag:_s) (Var pv) )) (Var pt) )) (Lit el) )) (
         (if (==( (index-of-tag el) unknown-index-of-tag )) () (tail(
            (set r CtxNil)
            (set r (CtxBind( (close r) pv (relocate( (
               (App(
                  (close(Lit(clone-rope(SAtom ':_s))))
                  (close(App(
                     (close(Lit (to-string(index-of-tag el)) ))
                     (close(AType(parse-type 'Constant+Literal+U64_s)))
                  )))
               ))
            ) loc )) )))
            (set r (CtxBind( (close r) pt
               (relocate( (Lit(clone-rope(SCons(
                  (close(SAtom 'Tag<_s))
                  (close(SCons(
                     (close(SAtom el))
                     (close(SAtom '>_s))
                  )))
               )))) loc ))
            )))
         )))
      ))
      ( (Pair( (Var pv) _ )) (
         (set r (CtxBind( (close CtxNil) pv (relocate( term loc )) )))
      ))
      ( _ () )
   ))
   r
)) Context);

relocate := λ(: term AST)(: loc SourceLocation). (: (tail(
   (let return term)
   (match term (
      ()
      ( ASTEOF () )
      ( ASTNil () )
      ( (Var v) (tail(
         (set v (clone-rope(SAtom v)))
         (map-location( v loc ))
         (set return (Var v))
      )))
      ( (Lit v) (tail(
         (set v (clone-rope(SAtom v)))
         (map-location( v loc ))
         (set return (Lit v))
      )))
      ( (AType _) () )
      ( (App( lt rt )) (
         (set return (App(
            (close(relocate( lt loc )))
            (close(relocate( rt loc )))
         )))
      ))
      ( (Abs( lt rt tlt )) (
         (set return (Abs(
            (close(relocate( lt loc )))
            (close(relocate( rt loc )))
            tlt
         )))
      ))
      ( (Seq( lt rt )) (
         (set return (Seq(
            (close(relocate( lt loc )))
            (close(relocate( rt loc )))
         )))
      ))
      ( (Typedef( lt rt )) (
         (set return (Typedef(
            (close(relocate( lt loc )))
            (close(relocate( rt loc )))
         )))
      ))
      ( (Glb( k t )) (tail(
         (set k (clone-rope(SAtom k)))
         (map-location( k loc ))
         (set return (Glb(
            k
            (close(relocate( t loc )))
         )))
      )))
      ( (Frg( k t tlt )) (
         (set return (Frg(
            (with-location( k loc ))
            (close(relocate( t loc )))
            tlt
         )))
      ))
   ))
   return
)) AST);

substitute-uuids := λ(: ctx Context)(: term AST). (: (tail(
   (let return term)
   (match return (
      ()
      ( (App( (Var 'uuid_s) (Var x ) )) (
         (while (non-zero ctx) (match ctx (
            ()
            ( (CtxBind( rst k v )) (
               (if (==( x k )) (tail(
                  (set return v)
                  (set ctx rst)
               )) (
                  (set ctx rst)
               ))
            ))
            ( _ (set ctx CtxEOF) )
         )))
      ))
      ( (App( l r )) (
         (set return (App(
            (close(substitute-uuids( ctx l )))
            (close(substitute-uuids( ctx r )))
         )))
      ))
      ( (Abs( l r tlt )) (
         (set return (Abs(
            (close(substitute-uuids( ctx l )))
            (close(substitute-uuids( ctx r )))
            tlt
         )))
      ))
      ( ASTEOF () )
      ( ASTNil () )
      ( (AType _) () )
      ( (Var v) () )
      ( (Lit v) () )
      ( (Seq( lt rt )) (
         (set return (Seq(
            (close(substitute-uuids( ctx lt )))
            (close(substitute-uuids( ctx rt )))
         )))
      ))
      ( (Typedef( lt rt )) (
         (set return (Typedef(
            (close(substitute-uuids( ctx lt )))
            (close(substitute-uuids( ctx rt )))
         )))
      ))
      ( (Glb( k t )) (
         (set return (Glb(
            k
            (close(substitute-uuids( ctx t )))
         )))
      ))
      ( (Frg( k t tlt )) (
         (set return (Frg(
            k
            (close(substitute-uuids( ctx t )))
            tlt
         )))
      ))
   ))
   return
)) AST);

extract-uuids := λ(: loc SourceLocation)(: ctx Context)(: term AST). (: (tail(
   (let return ctx)
   (match term (
      ()
      ( (App( (Var 'uuid_s) (Var x) )) (tail(
         (let id (uuid()))
         (map-location( id loc ))
         (let lctx (CtxBind( (close ctx) x (Var id) )))
         (set return lctx)
      )))
      ( (App( l r )) (tail(
         (let lctx (extract-uuids( loc ctx l )))
         (let rctx (extract-uuids( loc lctx r )))
         (set return rctx)
      )))
      ( (Abs( l r tlt )) (tail(
         (let lctx (extract-uuids( loc ctx l )))
         (let rctx (extract-uuids( loc lctx r )))
         (set return rctx)
      )))
      ( ASTEOF () )
      ( ASTNil () )
      ( (AType _) () )
      ( (Var v) () )
      ( (Lit v) () )
      ( (Seq( lt rt )) (tail(
         (let lctx (extract-uuids( loc ctx lt )))
         (let rctx (extract-uuids( loc lctx rt )))
         (set return rctx)
      )))
      ( (Typedef( lt rt )) (tail(
         (let lctx (extract-uuids( loc ctx lt )))
         (let rctx (extract-uuids( loc lctx rt )))
         (set return rctx)
      )))
      ( (Glb( k t )) (tail(
         (let rctx (extract-uuids( loc ctx t )))
         (set return rctx)
      )))
      ( (Frg( k t tlt )) (tail(
         (let rctx (extract-uuids( loc ctx t )))
         (set return rctx)
      )))
   ))
   return
)) Context);
