
fragment-substitute-context := λ ctx fragment-rhs . (match fragment-rhs (
   ()
   ( () () )
   ( (App( (Literal '.expression) (Variable v) )) (tail(
      (while ctx (
         (if (eq( v (head (tail ctx)) )) (
            (set v (expr::get-expr (tail (tail ctx))))
         ) ())
         (set ctx (head ctx))
      ))
      v
   )))
   ( (App( (Literal '.program) (Variable v) )) (tail(
      (while ctx (
         (if (eq( v (head (tail ctx)) )) (
            (set v (expr::get-prog (tail (tail ctx))))
         ) ())
         (set ctx (head ctx))
      ))
      v
   )))
   ( (Literal v) (
      (match v (
         ()
         (\\[ \[)
         (\\] \])
         (\\s \s)
         (\\t \t)
         (\\n \n)
         (u u)
      ))
   ))
   ( (App( l r )) (
      (fragment-substitute-context( ctx l ))
      (fragment-substitute-context( ctx r ))
   ))
   ( u (fail (UnknownFragmentSubstitution u)))
));

fragment-apply-context := λctx fragment-rhs e . (match fragment-rhs (
   ()
   ( Nil (
      e
   ))
   ( (App( (App( (Literal ':) fe )) ft )) (
      (fragment-apply-context( ctx fe e ))
   ))
   ( (App( (Literal '.program) f )) (
      (expr::set-prog( e (
         (expr::get-prog e)
         (fragment-substitute-context( ctx f ))
      )))
   ))
   ( (App( inner (App( (Literal '.program) f )) )) (
      (fragment-apply-context(
         ctx inner
         (expr::set-prog( e (
            (expr::get-prog e)
            (fragment-substitute-context( ctx f ))
         )))
      ))
   ))
   ( (App( (Literal '.expression) f )) (
      (expr::set-expr( e (
         (fragment-substitute-context( ctx f ))
      )))
   ))
   ( (App( inner (App( (Literal '.expression) f )) )) (
      (fragment-substitute-context(
         ctx inner
         (expr::set-expr( e (
            (fragment-substitute-context( ctx f ))
         )))
      ))
   ))
   ( u (fail (UnknownSubstituteFragment fragment-rhs)))
));

fragment-apply := λ ctx function-name function-type function-args e-proto . (tail(
   (assert-typeof( 'fragment-apply::e-proto e-proto StrictExpr ))
   (assert-typeof( 'fragment-apply::function-name function-name Atom ))
   (assert-typeof( 'fragment-apply::function-args function-args List<StrictExpr> ))
   (fragment-apply-direct(
      (get-strict-function( ctx function-name function-type ))
      function-args e-proto
   ))
));

fragment-apply-direct := λ fragment function-args e-proto . (tail(
   (assert-typeof( 'fragment-apply-direct::fragment fragment Fragment ))
   (assert-typeof( 'fragment-apply-direct::e-proto e-proto StrictExpr ))
   (assert-typeof( 'fragment-apply-direct::function-args function-args List<StrictExpr> ))
   (match fragment (
      ()
      ( (Fragment( _ (Lambda( lhs rhs )) )) (tail(
         (local fctx)
         (set fctx (fragment-destructure-lhs( () lhs function-args )))
         (local e-seed)
         (set e-seed (expr::new()))
         (set e-seed (expr::set-offset( e-seed (expr::get-offset e-proto) )))
         (set e-seed (expr::set-context( e-seed (expr::get-context e-proto) )))
         (fragment-apply-context( fctx rhs e-seed ))
      )))
      ( u (fail (InvalidFragment fragment)))
   ))
));

fragment-destructure-lhs := λ ctx lhs args . (tail(
   (assert-typeof( 'fragment-destructure-lhs::args args List<StrictExpr> ))
   (assert-not-typeof( 'fragment-destructure-lhs::args args Nil ))
   (match lhs (
      ()
      ( (App( (App( (Literal ':) (Variable vname) )) vtype )) (tail(
         (set ctx ( ctx (vname (tail args)) ))
         ctx
      )))
      ( (App( inner (App( (App( (Literal ':) (Variable vname) )) vtype )) )) (tail(
         (set ctx ( ctx (vname (tail args)) ))
         (fragment-destructure-lhs( ctx inner (head args) ))
      )))
      ( u (fail (PatternDefaultCase 'fragment-destructure-lhs u)))
   ))
));
