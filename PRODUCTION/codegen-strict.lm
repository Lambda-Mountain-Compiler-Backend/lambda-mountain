
compile-expr-strict := λctx e offset used . (tail(
   (assert-typeof( 'compile-expr-strict::offset offset Atom ))
   (assert-typeof( 'compile-expr-strict::used used Atom ))
   (local e1)
   (local e2)
   (local e3)
   (local e4)
   (match used (
      ()
      (Return (tail(
         (set e1 (compile-expr-strict( ctx e offset Used )))
         (fragment-apply( ctx 'cdecl::return (typecheck-lookup-type e) e1 e1 ))
      )))
      (_ (
         (match e (
            ()
            ( (App( (Lambda( (Variable lname) Nil )) rhs )) (tail(
               (local sz)
               (set sz (typecheck-sizeof (typecheck-lookup-type rhs)))
               (set ctx (ctx (StackVariable( lname (i2s(add( (s2i offset) (inv(s2i sz)) ))) (typecheck-lookup-type rhs) )) ))
               (set e1 (expr::new()) )
               (set e1 (expr::set-frame( e1 ( \t 'sub \s '$ sz , \s '%rsp \n ) )))
               (set e1 (expr::set-unframe( e1 ( \t 'add \s '$ sz , \s '%rsp \n ) )))
               (match (typecheck-lookup-type rhs) (
                  ()
                  ( (Cons( lt rt )) (tail(
                     (set e2 (expr::new()) )
                     (set e3 (expr::new()) )
                     (local at)
                     (set at (typecheck-lookup-type rhs))
                     (while at (
                        (local next-e2)
                        (local next-e3)
                        (match at (
                           ()
                           ( (Cons( lt rt )) (tail(
                              (match rhs (
                                 ()
                                 ( (App( inner outer )) (tail(
                                    (set offset (i2s (add( (s2i offset) (inv(s2i(typecheck-sizeof rt))) )) )) 
                                    (local edst)
                                    (set edst (expr::new()))
                                    (set edst (expr::set-expr( edst offset )))
                                    (set next-e2 (compile-expr-strict( ctx outer offset Used )))
                                    (set next-e3 (fragment-apply( ctx 'mov (Cons( (typecheck-lookup-type outer) LocalVariable )) ((() next-e2) edst) next-e2 )))
                                    (set rhs inner)
                                 )))
                                 ( u (fail( 'compile-strict::ArrayIndexAssign u )))
                              ))
                              (set at lt)
                           )))
                           ( rt (tail(
                              (set offset (i2s (add( (s2i offset) (inv(s2i(typecheck-sizeof rt))) )) )) 
                              (local edst)
                              (set edst (expr::new()))
                              (set edst (expr::set-expr( edst offset )))
                              (set next-e2 (compile-expr-strict( ctx rhs offset Used )))
                              (set next-e3 (fragment-apply( ctx 'mov (Cons( (typecheck-lookup-type rhs) LocalVariable )) ((() next-e2) edst) next-e2 )))
                              (set at ())
                           )))
                        ))
                        (set e2 (expr::chain( e2 next-e2 )))
                        (set e3 (expr::chain( e3 next-e3 )))
                     ))
                  ))) (_ (tail(
                     (set offset (i2s (add( (s2i offset) (inv(s2i sz)) )) )) 
                     (local edst)
                     (set edst (expr::new()))
                     (set edst (expr::set-expr( edst offset )))
                     (set e2 (compile-expr-strict( ctx rhs offset Used )))
                     (set e3 (fragment-apply( ctx 'mov (Cons( (typecheck-lookup-type rhs) LocalVariable )) ((() e2) edst) e2 )))
                  )))
               ))
               (set e3 (expr::set-frame( e3 (  (expr::get-frame e1) (expr::get-frame e2) (expr::get-frame e3) ))))
               (set e3 (expr::set-unframe( e3 (  (expr::get-unframe e1) (expr::get-unframe e2) (expr::get-unframe e3) ))))
               (set e3 (expr::set-text( e3 (  (expr::get-text e1) (expr::get-text e2) (expr::get-text e3) ))))
               (set e3 (expr::set-data( e3 (  (expr::get-data e1) (expr::get-data e2) (expr::get-data e3) ))))
               (set e3 (expr::set-prog( e3 (  (expr::get-prog e1) (expr::get-prog e2) (expr::get-prog e3) ))))
               (set e3 (expr::set-data( e3 (  (expr::get-data e1) (expr::get-data e2) (expr::get-data e3) ))))
               e3
            )))
            ( (App( (App( (Literal :) (Literal lval) )) ltype )) (tail(
               (set e1 (expr::set-expr( (expr::new ()) lval )))
               (set e1 (expr::set-context( e1 ctx )))
               (set e1 (expr::set-offset( e1 offset )))
               e1
            )))
            ( (Literal lval) (tail(
               (set e1 (expr::set-expr( (expr::new ()) lval )))
               (set e1 (expr::set-context( e1 ctx )))
               (set e1 (expr::set-offset( e1 offset )))
               e1
            )))
            ( (App( (App( (Literal :) lval )) ltype )) (tail(
               (set e1 (compile-expr-strict( ctx lval offset Unused )))
               e1
            )))
            ( (App( (Variable 'label) (Variable lname) )) (tail(
               (set e1 (compile-fragment( ctx e offset used )))
               e1
            )))
            ( (App( (Variable 'tail) (App( l r )) )) (tail(
               (set e1 (compile-expr-strict( ctx l offset Unused )))
               (set e2 (compile-expr-strict( (expr::get-context e1) r (expr::get-offset e1) Used )))
               (set e2 (expr::set-frame( e2 (  (expr::get-frame e1) (expr::get-frame e2)  ))))
               (set e2 (expr::set-unframe( e2 (  (expr::get-unframe e1) (expr::get-unframe e2)  ))))
               (set e2 (expr::set-text( e2 (  (expr::get-text e1) (expr::get-text e2)  ))))
               (set e2 (expr::set-data( e2 (  (expr::get-data e1) (expr::get-data e2)  ))))
               (set e2 (expr::set-prog( e2 (  (expr::get-prog e1) (expr::get-prog e2)  ))))
               e2
            )))
            ( (App ((Variable 'gensym-label) (Variable arg))) (tail(
               (set ctx (ctx (Label( arg (uuid()) )) ))
               (set e1 (expr::set-context( (expr::new()) ctx )))
               (set e1 (expr::set-offset( e1 offset )))
               e1
            )))
            ( (App ((Variable fname) arg)) (tail(
               (set e1 (stack-call( ctx fname arg offset )))
               e1
            )))
            ( Nil (tail(
               (set e1 (expr::new()) )
               (set e1 (expr::set-context( e1 ctx )))
               (set e1 (expr::set-offset( e1 offset )))
               e1
            )))
            ( (Variable v) (tail(
               (set e1 (fragment-get-local( ctx v offset )))
               e1
            )))
            ( (App (l r)) (tail(
               (if (eq( used Used )) (
                  (fail (TODO Return Strict Cons e))
               ) (
                  (set e1 (compile-expr-strict( ctx l offset Unused )))
                  (set e2 (compile-expr-strict( (expr::get-context e1) r (expr::get-offset e1) Used )))
                  (set e2 (expr::set-frame( e2 (  (expr::get-frame e1) (expr::get-frame e2)  ))))
                  (set e2 (expr::set-unframe( e2 (  (expr::get-unframe e1) (expr::get-unframe e2)  ))))
                  (set e2 (expr::set-text( e2 (  (expr::get-text e1) (expr::get-text e2)  ))))
                  (set e2 (expr::set-data( e2 (  (expr::get-data e1) (expr::get-data e2)  ))))
                  (set e2 (expr::set-prog( e2 (  (expr::get-prog e1) (expr::get-prog e2)  ))))
                  e2
               ))
            )))
            ( _ (fail (TODO CompileStrict e)))
         ))
      ))
   ))
));

define-calling-convention-stack := λ ctx fname lmb . (tail(
   ()
   (match lmb (
      ()
      ( (Lambda( lhs rhs )) (tail(
         (local e1)
         (local e2)
         (local text)
         (set e1 (define-calling-convention-stack-destructure-args( ctx lhs () )))
         (local stack_offset)
         (set stack_offset (expr::get-offset e1))
         (set stack_offset (i2s(add( (s2i(stack_offset)) (s2i '-8) ))) )
         (set e2 (compile-expr-strict( (expr::get-context e1) rhs stack_offset Return)))
         (set text ( text (mangle-global-function( fname lmb )) ': \n ))
         (set text ( text (expr::get-frame e2) ))
         (set text ( text (expr::get-prog e2) ))
         (set text ( text (expr::get-unframe e2) ))
         (set text ( text (\t 'ret \n) ))
         (set text ( text (expr::get-text e2) ))
         (local return)
         (set return (expr::new()))
         (set return (expr::set-text( return text )))
         (set return (expr::set-data( return (expr::get-data e2) )))
         return
      )))
   ))
));

define-calling-convention-stack-destructure-args := λ ctx lhs offset . (match lhs (
   ()
   ( (App( (App( (Literal :) (Variable lname) )) ltype )) (tail(
      (set ltype (typecheck-infer-type-compound ltype))
      (local size)
      (set size (typecheck-sizeof ltype))
      (local new_offset)
      (set new_offset (add( offset (inv(s2i size)) )) )
      (local return)
      (set return (expr::new()))
      (set return (expr::set-context( return 
         ( ctx (StackVariable( lname (i2s new_offset) ltype )) )
      )))
      (set return (expr::set-offset( return (i2s new_offset) )))
      return
   )))
   ( (App( more (App( (App( (Literal :) (Variable lname) )) ltype )) )) (tail(
      (set ltype (typecheck-infer-type-compound ltype))
      (local size)
      (set size (typecheck-sizeof ltype))
      (local new_offset)
      (set new_offset (add( offset (inv(s2i size)) )) )
      (set ctx (define-calling-convention-stack-destructure-args( ctx more new_offset )))
      (local return)
      (set return (expr::new()))
      (set return (expr::set-context( return 
         ( ctx (StackVariable( lname (i2s new_offset) ltype )) )
      )))
      (set return (expr::set-offset( return (i2s new_offset) )))
      return
   )))
   ( u (fail (UnknownStackArg lhs)))
));

compile-fragment := λctx e offset used . (match e (
   ()
   ( (StrictExpr _) e )
   ( (App( (App( (Variable 'as) inner_e )) _ )) (
      (compile-fragment( ctx inner_e offset used ))
   ))
   ( (App( (Variable 'label) (Variable lname) )) (tail(
      (set e (fragment-get-local( ctx lname offset )))
      (set e (expr::set-prog( (expr::new()) ((expr::get-expr e) ': \n) )))
      (set e (expr::set-context( e ctx )))
      (set e (expr::set-offset( e offset )))
      e
   )))
   ( (App( (Variable op) arg )) (tail(
      ()
      (match (get-maybe-function(ctx op (typecheck-lookup-type arg))) (
         ()
         ( (Fragment( (Variable _) body )) (tail(
            ()
            (fail (TODO AssembleApplyFragment 5)) 
            #(assemble-apply-fragment( ctx body arg offset ))
         )))
         ( v (
            (fail (ReferenceToUndefinedOperator op (typecheck-lookup-type arg)))
         ))
      ))
   )))
   ( (App( (App( (Literal :) (Literal lval) )) ltype )) (tail(
      (set ltype (typecheck-infer-type-compound ltype))
      (set e (expr::set-expr( (expr::new()) lval )))
      (set e (expr::set-context( e ctx )))
      (set e (expr::set-offset( e offset )))
      e
   )))
   ( (Variable v) (tail(
      ()
      (fragment-get-local( ctx v offset ))
   )))
   ( (Literal val) (tail(
      (set e (expr::set-expr( (expr::new()) val )))
      (set e (expr::set-context( e ctx )))
      (set e (expr::set-offset( e offset )))
      e
   )))
   ( u (fail (UnknownFragment e)))
);

get-strict-function := λctx v arg_type . (tail(
   (local r)
   (while ctx (
      (match (tail ctx) (
         ()
         ( (Fragment( (Variable name) body )) (tail(
            ()
            (if r () (
               if (eq(name v)) (
                  (if (typecheck-apply-plural( (typecheck-lookup-type body) arg_type )) (
                     (set r (Fragment( (Variable name) body )))
                  ) ())
               ) ()
            ))
         )))
         ( (GlobalVariable name) (
            if r () (
               if (eq(name v)) (
                  (set r (GlobalVariable (label-case name)))
               ) ()
            )
         ))
         ( (StackVariable(name offset type)) (
            if r () (
               if (eq(name v)) (
                  (set r (StackVariable( name offset type )))
               ) ()
            )
         ))
         ( u (fail( UnknownStrictFunction u )))
      ))
      (set ctx (head ctx))
   ))
   r
));
