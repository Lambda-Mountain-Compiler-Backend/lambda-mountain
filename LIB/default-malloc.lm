
# type MallocBlock = {
#    block-size : U64
#    block-prev : *MallocBlock
#    block-next : *MallocBlock
#    [data] : U8[block-size]
# }

.block-size := λ(: b MallocBlock[]). (: (
   (let r ([]( (as b U64[]) 0_u64 )))
   r
) U64);
.block-prev := λ(: b MallocBlock[]). (: (
   (let r ([]( (as b U64[]) 1_u64 )))
   (as r MallocBlock[])
) MallocBlock[]);
.block-next := λ(: b MallocBlock[]). (: (
   (let r ([]( (as b U64[]) 2_u64 )))
   (as r MallocBlock[])
) MallocBlock[]);
.block-data := λ(: b MallocBlock[]). (: (
   (as (+( (as b U64) 24_u64 )) ?[])
) ?[]);

set.block-size := λ(: b MallocBlock[])(: sz U64). (: (
   (set[]( (as b U64[]) 0_u64 sz ))
) Nil);
set.block-prev := λ(: b MallocBlock[])(: n MallocBlock[]). (: (
   (set[]( (as b U64[]) 1_u64 (as n U64) ))
) Nil);
set.block-next := λ(: b MallocBlock[])(: n MallocBlock[]). (: (
   (set[]( (as b U64[]) 2_u64 (as n U64) ))
) Nil);

malloc-block-head := 0_u64;
malloc-block-tail := 0_u64;
malloc-first-free := 0_u64;
malloc-page-tail := 0_u64;

malloc-new-page := λ. (: (
   (mov( 12_u64 RAX ))
   (mov( 0_u64 RDI ))
   (syscall())

   (mov( RAX R8 )) #current page break in R8
   (set malloc-block-head (as R8 U64))
   (set malloc-block-tail (as R8 U64))

   # 8 GB
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (add( 1073741824_u64 R8 ))
   (mov( 12_u64 RAX ))
   (mov( R8 RDI ))
   (syscall())

   (set malloc-page-tail (as R8 U64))

   # sys_brk initially zeroes memory for each process
   # so the first MallocBlock::header will be zero initialized
) Nil);

# increase size of data of previous allocated memory
mextend := λ(: ptr ?[])(: sz U64). (: (
   # add parameter for base data pointer that should be extended
   # if room to bump before next block, then do bump
   # else fail
   (let block (as (-( (as ptr U64) 24_u64 )) MallocBlock[]))

   (let data malloc-block-tail)

   (if (<=( sz (.free-space block) )) (
      (set.block-size( block (+( (.block-size block) sz )) ))
      (if (==( (as (.block-next block) U64) 0_u64 )) (
         (set malloc-block-tail (+( malloc-block-tail sz )))
      ) ())
   ) (
      (set data 0_u64)
   ))

   (as data ?[])
) ?[]);

.free-space := λ(: head MallocBlock[]). (: (
   (let space 0_u64)
   (if (==( (as (.block-next head) U64) 0_u64 )) (
      (set space 1073741824_u64)
   ) (
      (set space (-(
         (as (.block-next head) U64)
         (+(
            (as head U64)
            (+( 24_u64 (.block-size head) ))
         ))
      )))
   ))
   space
) U64);

malloc-sliver-size := 24_u64; # need to tune this to find empirical optima

malloc := λ (: sz U64) . (: (
   (if (==( malloc-block-head 0_u64 )) (malloc-new-page()) ())

   (let scan-head (as malloc-first-free MallocBlock[]))
   (if (==( (as scan-head U64) 0_u64 )) (set scan-head (as malloc-block-head MallocBlock[])) ())

   (while (>( (+( sz 24_u64 )) (.free-space scan-head) )) (
      (set scan-head (.block-next scan-head))
      (if (<=( sz malloc-sliver-size )) (
         (set malloc-first-free (as scan-head U64)) # don't scan for slivers
      ) ())
   ))

   # if no free space, then add new page to end
   # create new block
   #    block-size = sz
   #    block-prev = prev
   #    block-next = next
   # update prev.block-next = this
   # update next.block-prev = this
   # update malloc-first-free

   (let misalignment (%( malloc-block-tail 8_u64 )))
   (let alignment (%( (-( 8_u64 misalignment )) 8_u64 )))
   (set malloc-block-tail (+( malloc-block-tail alignment )))
   (let block (as malloc-block-tail MallocBlock[]))
   (let data (+( (as block U64) 24_u64 )))
   (set malloc-block-tail (+( data sz )))

   # initialize block
   (set.block-next( scan-head block ))
   (set.block-size( block sz ))
   (set.block-next( block (as 0_u64 MallocBlock[]) ))
   (if (==( (as block U64) (as scan-head U64) )) (
      (set.block-prev( block (as 0_u64 MallocBlock[]) ))
   ) (
      (set.block-prev( block scan-head ))
   ))

   (as data ?[])
) ?[]);
