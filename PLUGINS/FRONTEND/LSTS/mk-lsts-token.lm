
mk-lsts-token := Î»(: s SmartString). (: (
   (let us (untern s))
   # if starts with 0-9 or -
   (if (||( (.is-digit us)
       (&&( (.has-prefix( us '-_s )) (.is-digit(tail-string us)) )) )) (
      (let ds us)
      (let is-negative 0_u64)
      (let order 64_u64)
      (if (==( ds '-9223372036854775808_s )) (
         (set is-negative 1_u64)
      ) (
         (if (.has-prefix( ds '-_s )) (
            (set is-negative 1_u64)
            (set ds (tail-string ds))
         ) ())
         (let magnitude (to-u64 ds))
         (if (||(
            (&&( is-negative (<=( magnitude 128_u64 )) ))
            (<=( magnitude 255_u64 ))
         )) (
            (set order 8_u64)
         ) (
            (if (||(
               (&&( is-negative (<=( magnitude 32768_u64 )) ))
               (<=( magnitude 65535_u64 ))
            )) (
               (set order 16_u64)
            ) (
               (if (||(
                  (&&( is-negative (<=( magnitude 2147483648_u64 )) ))
                  (<=( magnitude 4294967295_u64 ))
               )) (
                  (set order 32_u64)
               ) ())
            ))
         ))
      ))
      (match (Tuple( is-negative order )) (
         ()
         ( (Tuple( 0_u64 8_u64 )) (set us (+( us '_u8_s ))) )
         ( (Tuple( 1_u64 8_u64 )) (set us (+( us '_i8_s ))) )
         ( (Tuple( 0_u64 16_u64 )) (set us (+( us '_u16_s ))) )
         ( (Tuple( 1_u64 16_u64 )) (set us (+( us '_i16_s ))) )
         ( (Tuple( 0_u64 32_u64 )) (set us (+( us '_u32_s ))) )
         ( (Tuple( 1_u64 32_u64 )) (set us (+( us '_i32_s ))) )
         ( (Tuple( 0_u64 64_u64 )) (set us (+( us '_u64_s ))) )
         ( (Tuple( 1_u64 64_u64 )) (set us (+( us '_i64_s ))) )
      ))
   ) ())
   (if (.has-prefix( s 'r/_ss )) (
      (set us (.remove-prefix( us 'r/_s )))
      (set us (.remove-suffix( us '/_s )))
      (set us (.replace( us '\\/_s '/_s )))
      (set us (+( us '_rgx_s )))
   ) ())
   (if (.has-prefix( s '"_ss )) (
      (set us (.remove-prefix( us '"_s )))
      (set us (.remove-suffix( us '"_s )))
      (set us (.replace( us '\\"_s '"_s )))
      (set us (+( us '_ss_s )))
   ) ())
   (if (.has-prefix( s 'c"_ss )) (
      (set us (.remove-prefix( us 'c"_s )))
      (set us (.remove-suffix( us '"_s )))
      (set us (.replace( us '\\"_s '"_s )))
      (set us (+( us '_s_s )))
   ) ())
   (if (.has-prefix( s 'l"_ss )) (
      (set us (.remove-prefix( us 'l"_s )))
      (set us (.remove-suffix( us '"_s )))
      (set us (.replace( us '\\"_s '"_s )))
      (set us (+( us '_l_s )))
   ) ())
   (Token( s us (iuid()) (.location s) ))
) Token);
