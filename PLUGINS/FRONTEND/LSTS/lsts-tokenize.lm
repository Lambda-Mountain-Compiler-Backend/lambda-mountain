
macro ('lsts-try-tokenize-keep pat) (
   (if (&&( (!=( (head-string text) 0_u8 )) (has-prefix( text pat )) )) (
      (set tokens (cons(
         (with-location( pat (SourceLocation( fp current-line current-column )) )) tokens
      )))
      (set text (remove-prefix( text pat )))
      (set current-column (+( current-column (.length pat) )))
      (print 'Token:\s_s)(print pat)(print '\n_s)
   ) ())
);
macro ('lsts-try-tokenize-discard pat) (
   (if (&&( (!=( (head-string text) 0_u8 )) (has-prefix( text pat )) )) (
      (set text (remove-prefix( text pat )))
      (if (==( pat '\n_s )) (
         (set current-line (+( current-line 1_u64 )))
         (set current-column 0_u64)
      ) (
         (set current-column (+( current-column (.length pat) )))
      ))
   ) ())
);

lsts-tokenize := λ(: fp String). (: (
   (let text (read-file fp))
   (let tokens (: LEOF List<Token>))
   (let current-line 1_u64)
   (let current-column 1_u64)
   (while (head-string text) (
      (let prev-text text)
      (lsts-try-tokenize-keep '~=_s)
      (lsts-try-tokenize-keep '+=_s)
      (lsts-try-tokenize-keep '-=_s)
      (lsts-try-tokenize-keep '*=_s)
      (lsts-try-tokenize-keep '/=_s)
      (lsts-try-tokenize-keep '%=_s)
      (lsts-try-tokenize-keep '&=_s)
      (lsts-try-tokenize-keep '|=_s)
      (lsts-try-tokenize-keep '<=_s)
      (lsts-try-tokenize-keep '>=_s)
      (lsts-try-tokenize-keep '!=_s)
      (lsts-try-tokenize-keep '==_s)
      (lsts-try-tokenize-keep '<_s)
      (lsts-try-tokenize-keep '>_s)
      (lsts-try-tokenize-keep '{_s)
      (lsts-try-tokenize-keep '}_s)
      (lsts-try-tokenize-keep '[_s)
      (lsts-try-tokenize-keep ']_s)
      (lsts-try-tokenize-keep '\[_s)
      (lsts-try-tokenize-keep '\]_s)
      (lsts-try-tokenize-keep ':_s)
      (lsts-try-tokenize-keep '\:_s)
      (lsts-try-tokenize-keep ',_s)
      (lsts-try-tokenize-keep '?_s)
      (lsts-try-tokenize-keep '~_s)
      (lsts-try-tokenize-keep '@_s)
      (lsts-try-tokenize-keep '+_s)
      (lsts-try-tokenize-keep '-_s)
      (lsts-try-tokenize-keep '*_s)
      (lsts-try-tokenize-keep '/_s)
      (lsts-try-tokenize-keep '%_s)
      (lsts-try-tokenize-keep '&_s)
      (lsts-try-tokenize-keep '|_s)
      (lsts-try-tokenize-keep '<_s)
      (lsts-try-tokenize-keep '>_s)
      (lsts-try-tokenize-keep '!_s)
      (lsts-try-tokenize-keep '=_s)
      (lsts-try-tokenize-discard '\s_s)
      (lsts-try-tokenize-discard '\t_s)
      (lsts-try-tokenize-discard '\n_s)
      (lsts-try-tokenize-keep '._s) # can be part of an ident literal, so check this last
      (if (is-ident-head text) (
         (let ident-buffer SNil)
         (let ident-line current-line)
         (let ident-column current-column)
         (while (is-ident-head text) (
            (if (has-prefix( text '$"_s )) (
               (set text (tail-string text))
               (set text (tail-string text))
               (set ident-buffer (+( ident-buffer (SAtom '$"_s) )))
               (while (&&( (!=( (head-string text) 0_u8 )) (!=( (head-string text) 34_u8 )) )) (
                  (set ident-buffer (+( ident-buffer (SAtom(clone-rope(head-string text))) )))
                  (set text (tail-string text))            
               ))
               (if (==( (head-string text) 34_u8 )) (
                  (set ident-buffer (+( ident-buffer (SAtom '"_s) )))
                  (set text (tail-string text))            
               ) ())
            ) (
               (set ident-buffer (+( ident-buffer (SAtom(clone-rope(head-string text))) )))
               (set text (tail-string text))
            ))
         ))
         (let ident-text (clone-rope ident-buffer))
         (set current-column (+( current-column (.length ident-text) )))
         (print 'Token:\s_s)(print ident-text)(print '\n_s)
         (set tokens (cons(
            (with-location( ident-text (SourceLocation( fp ident-line ident-column )) )) tokens
         )))
      ) ())
      (if (is( text prev-text )) (
         (print 'Unrecognized\sToken\sStart\sIn\sFile\s_s)(print fp)
         (print current-line)(print ',_s)(print current-column)(print ':\s\`_s)(print(clone-rope(head-string text)))(print '\`_s)(print '\n_s)(exit 1_u64)
      ) ())
   ))
   (reverse tokens)
) List<Token>);

is-ident-head := λ(: text String). (: (
   (let r 0_u64)
   (if (&&( (>=( (head-string text) 48_u8 )) (<=( (head-string text) 57_u8 )) )) (set r 1_u64) ())  # 0-9
   (if (&&( (>=( (head-string text) 65_u8 )) (<=( (head-string text) 90_u8 )) )) (set r 1_u64) ())  # A-Z
   (if (&&( (>=( (head-string text) 97_u8 )) (<=( (head-string text) 122_u8 )) )) (set r 1_u64) ()) # a-z
   (if (==( (head-string text) 95_u8 )) (set r 1_u64) ()) # _
   (if (==( (head-string text) 46_u8 )) (set r 1_u64) ()) # .
   (if (==( (head-string text) 36_u8 )) (set r 1_u64) ()) # $
   r
) U64);

is-lit-head := λ(: text String). (: (
   (let r 0_u64)
   (if (&&( (>=( (head-string text) 48_u8 )) (<=( (head-string text) 57_u8 )) )) (set r 1_u64) ())  # 0-9
   (if (&&( (>=( (head-string text) 65_u8 )) (<=( (head-string text) 90_u8 )) )) (set r 1_u64) ())  # A-Z
   r
) U64);
