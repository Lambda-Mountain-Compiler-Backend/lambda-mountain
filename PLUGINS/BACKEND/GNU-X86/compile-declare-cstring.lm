
cstring-cache := (: SSEOF StringStringList);
compile-declare-cstring := λ(: val String). (: (
   (let r (fragment::new()))
   (match config-assemble-mode (
      ()
      ( AssembleC (set r (compile-declare-cstring-c( val ))))
      ( AssembleGNU (set r (compile-declare-cstring-gnu( val ))))
      ( AssembleBlob (set r (compile-declare-cstring-blob( val ))))
   ))
   r
) Fragment);

compile-declare-cstring-blob := λ(: val String). (: (
   (let r (fragment::new()))
   (print 'TODO\sdeclare\scstring\sblob\n_s)(exit 1_u64)
   r 
) Fragment);

compile-declare-cstring-c := λ(: val String). (: (
   (let r (fragment::new()))
   (set r (.set( r 'expression_s (
      (+(
         (SAtom '"_s)
         (+(
            (escape-c-string val)
            (SAtom '"_s)
         ))
      ))
   ))))
   r 
) Fragment);

escape-c-string := λ(: s String). (: (
   (let e SNil)
   (while (head-string s) (
      (if (==( (head-string s) 92_u8 )) (
         (set s (tail-string s))
         (match (head-string s) (
            ()
            ( 58_u8 (set e (+( e (SAtom '\:_s) ))) )
            ( 91_u8 (set e (+( e (SAtom '\[_s) ))) )
            ( 92_u8 (set e (+( e (SAtom '\\\\\\\\_s) ))) )
            ( 93_u8 (set e (+( e (SAtom '\]_s) ))) )
            ( 96_u8 (set e (+( e (SAtom '\`_s) ))) )
            ( 108_u8 (set e (+( e (SAtom '\l_s) ))) )
            ( 110_u8 (set e (+( e (SAtom '\\\\n_s) ))) )
            ( 111_u8 (set e (+( e (SAtom '\o_s) ))) )
            ( 115_u8 (set e (+( e (SAtom '\s_s) ))) )
            ( 116_u8 (set e (+( e (SAtom '\\\\t_s) ))) )
            ( c (
               (print 'Illegal\sCharacter:\s_s)(print c)(print '\sin\s_s)(print s)(print '\n_s)
               (fail 'Illegal\sEscape\sSequence\sIn\sString\s_s)
            ))
         ))
         (set s (tail-string s))
      ) (
         (if (==( (head-string s) 34_u8 )) (
            (set e (+( e (SAtom '\\\\"_s) )))
         ) (
            (set e (+( e (SAtom(clone-rope(head-string s))) )))
         ))
         (set s (tail-string s))
      ))
   ))
   e
) S);

compile-declare-cstring-gnu := λ(: val String). (: (
   (let r (fragment::new()))
   (if (is( config-assemble-mode AssembleBlob )) (
      (set r (.set( r 'expression_s (SAtom val) )))
   ) (
      (let cache cstring-cache)
      (let id '_s)
      (while (non-zero cache) (match cache (
         ()
         ( (SSSeq( rst k v )) (
            (if (==( k val )) (
               (set id v)
               (set cache SSEOF)
            ) (set cache rst))
         ))
      )))
      (if (head-string id) () (
         (set id (uuid()))
         (set assemble-data-section (SCons( (close assemble-data-section) (close(SAtom id)) )))
         (set assemble-data-section (SCons( (close assemble-data-section) (close(SAtom ':\n\t.ascii\s"_s)) )))
         (set assemble-data-section (SCons( (close assemble-data-section) (close(SAtom( escape-literal val ))) )))
         (set assemble-data-section (SCons( (close assemble-data-section) (close(SAtom '"\n\t.zero\s1\n_s)) )))
         (set cstring-cache (SSSeq(
            (close cstring-cache) val id
         )))
      ))
      (set r (.set( r 'expression_s (SAtom id) )))
   ))
   r
) Fragment);
