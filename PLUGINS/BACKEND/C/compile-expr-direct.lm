
compile-smart-string-index := (: (HashtableEq( 0_u64 0_u64 (as 0_u64 Tuple<String,String>[]) )) HashtableEq<String,String>);

compile-expr-direct := Î»(: ctx FContext)(: term AST)(: stack-offset I64)(: used IsUsed). (: (
   (let e (mk-fragment()))
   (set.context( e (close ctx) ))
   (match term (
      ()
      ( (Var( id _ )) (
         # TODO, this is being called instead of function
         (let def (.lookup( var-to-def-index term ASTEOF )))
         (if (not(non-zero def)) (
            (exit-error( (+( 'Unable\sTo\sFind\sVariable\sIn\sContext\s_s id )) term ))
         ) ())
         (set e (.lookup( std-c-fragment-context def e )))
         (if (not(non-zero e)) (
            (exit-error( (+( 'Unable\sTo\sFind\sVariable\sFragment\sIn\sContext\s_s id )) term ))
         ) ())
         (set.context( e (close ctx) ))
      ))
      ( (Lit( val _ )) (
         (let ltype (typeof term))
         (if (||( (||( (.is-t( ltype 'String_s )) (.is-t( ltype 'SmartString_s )) )) (.is-t( ltype 'Regex_s )) )) (
            (if (.is-t( ltype 'String_s )) (
               (set e (compile-declare-cstring( val )))
            ) ())
            (if (.is-t( ltype 'SmartString_s )) (
               (let guid (.lookup( compile-smart-string-index val '_s )))
               (if (non-zero guid) (
                  (set e (.set( e 'expression_s (SAtom guid) )))
               ) (
                  (set guid (uuid()))
                  (set e (.set( e 'expression_s (SAtom guid) )))
                  (set compile-smart-string-index (.bind( compile-smart-string-index val guid )))
                  (let lit (Lit( val (mk-token val) )))
                  (ascript-normal( lit (t1 'String_s) ))
                  (let intern-ss (compile-stack-calls( ctx 'intern_s TAny (typeof term) lit stack-offset Used )))
                  (set assemble-header-section (+(
                     assemble-header-section
                     (+(
                        (+( (mangle-c-type ltype) (SAtom '\s_s) ))
                        (+( (SAtom guid) (SAtom '\:\n_s) ))
                     ))
                  )))
                  (set assemble-string-initializer-section (+(
                     assemble-string-initializer-section
                     (+(
                        (+( (SAtom guid) (SAtom '=_s) ))
                        (+( (.get( intern-ss 'expression_s )) (SAtom '\:\n_s) ))
                     ))
                  )))
               ))
            ) ())
            (if (.is-t( ltype 'Regex_s )) (
               (let rgx-id (uuid()))
               (set assemble-header-section (+(
                  assemble-header-section
                  (+( (+( (SAtom 'regex_t\s_s) (SAtom rgx-id) )) (SAtom '\:\n_s) ))
               )))
               (set assemble-global-initializer-section (+(
                  assemble-global-initializer-section
                  (+( (+( (+( (SAtom '{int\sreturn_code\s=\sregcomp\[&_s) (SAtom rgx-id) ))
                      (+( (SAtom ',\s"_s) (escape-c-string val) )) ))
                          (SAtom '",\sREG_EXTENDED\]\:if\[return_code\]{fprintf\[stderr,"Could\snot\scompile\sregex."\]\:exit\[1\]\:}}\:\n_s) ))
               )))
               (set e (mk-expression rgx-id))
            ) ())
         ) (
            (let isa-fragment False_u8)
            (if (==( isa-fragment True_u8 )) (
               (set e (mk-expression val))
            ) (
               (if (.is-lm-struct(.with-only-class(typeof term))) (
                  (let tag-index (index-of-tag val))
                  (set e (.set( e 'expression_s (
                     (+(
                        (+(
                           (SAtom '\[{_s)
                           (mangle-c-type ltype)
                        ))
                        (+(
                           (SAtom '\srvalue={_s)
                           (+(
                              (SAtom(to-string tag-index))
                              (SAtom '}\:rvalue\:}\]_s)
                           ))
                        ))
                     ))
                  ))))
               ) (
                  (match val (
                     ()
                     ( 'True_s (set e (mk-expression '\[1\]_s)) )
                     ( 'False_s (set e (mk-expression '\[0\]_s)) )
                     ( '\\t_s (set e (mk-expression '\[9\]_s)) )
                     ( '\\n_s (set e (mk-expression '\[10\]_s)) )
                     ( '\\s_s (set e (mk-expression '\[32\]_s)) )
                     ( '\\o_s (set e (mk-expression '\[35\]_s)) )
                     ( '\\`_s (set e (mk-expression '\[39\]_s)) )
                     ( '\\[_s (set e (mk-expression '\[40\]_s)) )
                     ( '\\]_s (set e (mk-expression '\[41\]_s)) )
                     ( '\\:_s (set e (mk-expression '\[59\]_s)) )
                     ( _ (
                        (if (.is-t( ltype 'L_s )) (
                           (set e (.set( e 'expression_s (SAtom val) )))
                        ) (
                           (set e (.set( e 'expression_s (
                              (+( (+( (SAtom '\[_s) (SAtom val) )) (SAtom '\]_s) ))
                           ))))
                        ))
                     ))
                  ))
               ))
            ))
         ))
         (set.context( e (close ctx) ))
         (set.type( e (denormalize ltype) ))
      ))
      ( (App( (Lit( ':_s _ )) (App( t (AType tt) )) )) (
         (set e (compile-expr( ctx t stack-offset used )))
      ))
      ( (App( (Var( 'scope_s _ )) t )) (
         (set e (compile-expr( ctx t stack-offset Tail )))
         (set.context( e (close ctx) ))
      ))
      ( (App( (Var( 'open_s _ )) t )) (
         (let tt (typeof t))
         (if (.is-t( tt 'Array_s )) (
            (match (.slot( tt 'Array_s )) (
               ()
               ( (TGround( 'Array_s (LCons( _ (LCons( TAny LEOF )) )) )) (
                  (set e (compile-expr( ctx t stack-offset Used )))
               ) )
               ( (TGround( 'Array_s (LCons( TAny (LCons( inner-tt LEOF )) )) )) (
                  (set e (compile-stack-calls( ctx 'open_s TAny (t1 'Nil_s) t stack-offset Used )))
               ))
               ( (TGround( 'Array_s (LCons( array-length (LCons( inner-tt LEOF )) )) )) (
                  (set e (compile-expr( ctx t stack-offset Used )))
               ))
            ))
         ) (
            (set e (compile-stack-calls( ctx 'open_s TAny (t1 'Nil_s) t stack-offset Used )))
         ))
      ))
      ( (App( (Var( 'sizeof_s _ )) (AType tt) )) (
         (set e (mk-expression(
            (+( (SAtom '\[sizeof\[_s)
                (+( (mangle-c-type tt) (SAtom '\]\]_s) ))
            ))
         )))
         (let et (TAnd( 
            (close(t1 'Literal_s))
            (close(TAnd(
               (close(t1 'Constant_s))
               (close(t1 'U64_s))
            )))
         )))
         (set.type( e et ))
         (set.context( e (close ctx) ))
      ))
      ( (App( (Var( 'as_s _ )) (App( t (AType tt) )) )) (
         (set e (compile-expr( ctx t stack-offset used )))
         (set.type( e (typeof term) ))
         (set e (.set( e 'expression_s (
            (+(
               (+(
                  (SAtom '\[\[_s)
                  (mangle-c-type (typeof term))
               ))
               (+(
                  (SAtom '\]\[_s)
                  (+(
                     (.get( e 'expression_s ))
                     (SAtom '\]\]_s)
                  ))
               ))
            ))
         ))))
      ))
      ( (App( (App( (App( (Var( 'if_s _ )) cond )) t )) f )) (
         (let rsp-offset (+( stack-offset -8_i64 )))
         (let ec (cc-blob( ctx 'into-branch-conditional_s cond rsp-offset )))
         (let et (compile-expr( (open(.context ec)) t 0_i64 Tail )))
         (let end-offset 0_i64)
         (set.type( et (typeof t) ))
         (let ef (compile-expr( (open(.context ec)) f 0_i64 Tail )))
         (set.type( ef (typeof f) ))
         (if (.is-t( (typeof f) 'Never_s )) (
            (set ef (never-as-expr-type( ef (typeof t) )))
         ) ())

         (let ictx (mk-fragment()))
         (set.type( ictx (t1( 'ImplicitContext_s )) ))
        
         (set ctx (.bind( ctx 'ictx_s (t1 'ImplicitContext_s) ictx )))
         (set ctx (.bind( ctx 'c_s (t1 'BranchConditional_s) ec )))
         (set ctx (.bind( ctx 't_s (typeof t) et )))
         (set ctx (.bind( ctx 'f_s (typeof f) ef )))
         (let a-tt (t3( 'Cons_s 
            (t3( 'Cons_s 
               (t3( 'Cons_s (t1 'ImplicitContext_s) (typeof f) ))
               (typeof t)
            ))
            (t1 'BranchConditional_s)
         )))

         (set e (cc-blob( ctx 'primitive::if_s a-tt stack-offset term )))

         (set.context( e (.context et) ))
         (set.type( e (typeof term) ))
      ))
      ( (App( (Abs( (@( lhs-t (Var( lhs _ )) )) ASTNil tlt )) rhs )) (
         (let rtype (.without-modifiers(typeof lhs-t)))
         (let fr (fragment::local-variable( 0_i64 rtype )))
         (set std-c-fragment-context (.bind( std-c-fragment-context lhs-t fr )))
         (set ctx (.bind( ctx lhs rtype fr )))
         (let prepost (mangle-c-declaration rtype))
         (if (is-const-array rtype) (
            (set e (compile-expr( ctx rhs stack-offset Used )))
            (set e (.set( e 'frame_s (
               (+(
                  (+(
                     (.first prepost)
                     (SAtom '\s_s)
                  ))
                  (+(
                     (.get( fr 'expression_s ))
                     (+(
                        (+(
                           (.second prepost)
                           (SAtom '=_s)
                        ))
                        (+(
                           (.get( e 'expression_s ))
                           (SAtom '\:\n_s)
                        ))
                     ))
                  ))
               ))
            ))))
            (set e (.set( e 'expression_s SNil )))
         ) (
            (match rhs (
               ()
               ( (App( (Lit( ':_s _ )) (App( (Var( '__uninitialized_s _ )) (AType tt) )) )) () )
               ( _ (
                  (set e (compile-expr( ctx rhs stack-offset Used )))
                  (set e (.set( e 'expression_s (
                     (+(
                        (+(
                           (SAtom '\[{_s)
                           (.get( fr 'expression_s ))
                        ))
                        (+(
                           (SAtom '=_s)
                           (+(
                              (.get( e 'expression_s ))
                              (SAtom '\:\[{}\]\:}\]\n_s)
                           ))
                        ))
                     ))
                  ))))
               ))
            ))
            (set e (.set( e 'frame_s (
               (+(
                  (.get( e 'frame_s ))
                  (+(
                     (+(
                        (.first prepost)
                        (SAtom '\s_s)
                     ))
                     (+(
                        (.get( fr 'expression_s ))
                        (+(
                           (.second prepost)
                           (SAtom '\:\n_s)
                        ))
                     ))
                  ))
               ))
            ))))
         ))
         (set.context( e (close ctx) ))
         (set.type( e (typeof term) ))
      ))
      ( (App( is-cons f a )) (
         (if (&&( (not is-cons) (.is-arrow(typeof f)) )) (
            (match f (
               ()
               ( (Var( fname _ )) (
                  (set e (compile-stack-calls( ctx fname (typeof f) (typeof term) a stack-offset used )))
               ))
               ( (App( (Lit( ':_s _ )) (App( (Var( fname _ )) (AType ft) )) )) (
                  (set e (compile-stack-calls( ctx fname (typeof f) (typeof term) a stack-offset used )))
               ))
               ( (Lit( fname _ )) (
                  (set e (compile-constructor( ctx fname (typeof term) (typeof a) a stack-offset )))
               ))
               ( (App( (Lit( ':_s _ )) (App( (Lit( fname _ )) (AType ft) )) )) (
                  (set e (compile-constructor( ctx fname (typeof term) (typeof a) a stack-offset )))
               ))
               ( _ (
                  (let call (compile-expr( ctx f stack-offset Unused )))
                  (let args (compile-push-rvalue( ctx a stack-offset )))
                  (set e (fragment::chain( call args )))
                  (set e (.set( e 'expression_s (+(
                     (+( (.get( call 'expression_s )) (SAtom '\[_s) ))
                     (+( (.get( args 'expression_s )) (SAtom '\]_s) ))
                  )))))
               ))
            ))
         ) (
            (if (is( used Unused )) (scope(
               (let e1 (compile-expr( ctx f stack-offset Unused )))
               (let e2 (compile-expr( (open(.context e1)) a 0_i64 Unused )))
               (set e (fragment::chain( e1 e2 )))
               (set.type( e (typeof term) ))
            )) (scope(
               (let e1 (compile-expr( ctx f stack-offset Unused )))
               (let e2 (compile-expr( (open(.context e1)) a 0_i64 Used )))
               (set e (fragment::chain( e1 e2 )))
               (set.type( e (typeof term) ))
            )))
         ))
      ))
      ( _ (set e (std-c-compile-expr( ctx term false ))) )
   ))
   e
) Fragment);

