
index-c-type-ordinal := (: (HashtableEq( 0_u64 0_u64 (as 0_u64 Tuple<Type,String>[]) )) HashtableEq<Type,String>);

compile-c-typedef-concrete-ordinal := λ(: base-type Type)(: blame AST). (: (
   (let base-uuid (uuid()))
   (set index-c-type-ordinal (.bind( index-c-type-ordinal base-type base-uuid )))
   (set assemble-header-section (SCons( (close assemble-header-section) (close(SAtom 'typedef\sstruct\s_s)) )))
   (set assemble-header-section (SCons( (close assemble-header-section) (close(mangle-c-type( base-type blame ))) )))
   (set assemble-header-section (SCons( (close assemble-header-section) (close(SAtom '\s_s)) )))
   (set assemble-header-section (SCons( (close assemble-header-section) (close(mangle-c-type( base-type blame ))) )))
   (set assemble-header-section (SCons( (close assemble-header-section) (close(SAtom '\:\n_s)) )))
) Nil);

compile-c-typedef-ordinal := λ(: base-type Type)(: blame AST). (: (
   (if (.is-lm-struct base-type) (
      (if (.is-open base-type) () (
         (compile-c-typedef-concrete-ordinal( base-type blame ))
      ))
      (match base-type (
         ()
         ( (TGround( tag ts )) (
            (let k (Tuple( tag (.length ts) )))
            (for-each (vt in (.lookup( concrete-type-instances-index k (: LEOF List<Type>) ))) (
               (compile-c-typedef-concrete-ordinal( vt blame ))
            ))
         ))
      ))
   ) ())
) Nil);

continue-compile-c-typedefs-concrete := (: LEOF List<Tuple<Tuple<Maybe<TContext>,Type>,AST>>);
continue-compile-c-typedefs-count := 0_u64;
try-continue-compile-c-typedefs := λ. (: (
   (let continue continue-compile-c-typedefs-concrete)
   (set continue-compile-c-typedefs-concrete (: LEOF List<Tuple<Tuple<Maybe<TContext>,Type>,AST>>))
   (while (non-zero continue) (
      (set continue-compile-c-typedefs-count (+( continue-compile-c-typedefs-count 1_u64 )))
      (if (>( continue-compile-c-typedefs-count 100_u64 )) (
         (print 'Unable\sTo\sResolve\sCircular\sType\sDependency\sDuring\sCompilation\n_s)
         (for-each ((Tuple( (Tuple( _ base-type )) td )) in continue) (
            (print 'Typedef:\s_s)(print base-type)(print '\n_s)
         ))
         (exit 1_u64)
      ) ())
      (for-each ( (Tuple( (Tuple( tctx base-type )) body )) in continue ) (
         (if (.is-open base-type) () (
            (compile-c-typedef-concrete( tctx base-type body ))
         ))
      ))
      (set continue continue-compile-c-typedefs-concrete)
      (set continue-compile-c-typedefs-concrete (: LEOF List<Tuple<Tuple<Maybe<TContext>,Type>,AST>>))
   ))
) Nil);

is-incomplete-typedef := λ(: tt Type). (: (
   (let r 0_u64)
   (match tt (
      ()
      ( (TGround( 'Cons_s (LCons( p1 (LCons( p2 LEOF )) )) )) (
         (set r (||( (is-incomplete-typedef p1) (is-incomplete-typedef p2) )))
      ))
      ( (TAnd( conjugate )) (
         (for-each-v (c in conjugate) (set r (||( r (is-incomplete-typedef c) ))))
      ))
      ( TAny () )
      ( (TVar _) () )
      ( (TGround( 'Nil_s _ )) () )
      ( (TGround( 'Never_s _ )) () )
      ( (TGround( 'U8_s _ )) () )
      ( (TGround( 'U16_s _ )) () )
      ( (TGround( 'U32_s _ )) () )
      ( (TGround( 'U64_s _ )) () )
      ( (TGround( 'I8_s _ )) () )
      ( (TGround( 'I16_s _ )) () )
      ( (TGround( 'I32_s _ )) () )
      ( (TGround( 'I64_s _ )) () )
      ( (TGround( 'String_s _ )) () )
      ( (TGround( 'File_s _ )) () )
      ( (TGround( 'Array_s _ )) () )
      ( (TGround( 'Arrow_s _ )) () )
      ( (TGround( _ _ )) (
         (if (.lookup( is-cstruct-hard-compiled-index tt 0_u64 )) () (
            (set r 1_u64)
         ))
      ))
   ))
   r
) U64);

is-cstruct-hard-compiled-index := (: (HashtableEq( 0_u64 0_u64 (as 0_u64 Tuple<Type,U64>[]) )) HashtableEq<Type,U64>);
compile-c-typedef-concrete := λ(: tctx Maybe<TContext>)(: base-type Type)(: body AST). (: (
   (if (.is-open base-type) () (
      (let original-body body)
      (let base-uuid (.lookup( index-c-type-ordinal base-type 'UnknownCTypename_s )))
      (let case-number 0_u64)
      (let cases SNil)
      (let incomplete 0_u64)
      (let has-case 0_u64)
      (while (non-zero body) (match body (
         ()
         ( (App( (App( rst (Var( '|_s _ )) )) case-definition )) (
            (let fields (compile-c-fields-of-case( base-type case-definition case-number )))
            (set fields (substitute( tctx fields )))
            (set incomplete (||( incomplete (is-incomplete-typedef fields) )))
            (if (.is-t( fields 'Nil_s )) () (
               (set cases (+( cases (compile-c-casedef( tctx fields case-number body )) )))
               (set has-case 1_u64)
            ))
            (set case-number (+( case-number 1_u64 )))
            (set body rst)
         ))
         ( case-definition (
            (let fields (compile-c-fields-of-case( base-type case-definition case-number )))
            (set fields (substitute( tctx fields )))
            (set incomplete (||( incomplete (is-incomplete-typedef fields) )))
            (if (.is-t( fields 'Nil_s )) () (
               (set cases (+( cases (compile-c-casedef( tctx fields case-number body )) )))
               (set has-case 1_u64)
            ))
            (set body ASTEOF)
         ))
      )))
      (if incomplete (
         (set continue-compile-c-typedefs-concrete (cons(
            (Tuple( (Tuple( tctx base-type )) original-body ))
            continue-compile-c-typedefs-concrete
         )))
      ) (
         (set is-cstruct-hard-compiled-index (.bind( is-cstruct-hard-compiled-index base-type 1_u64 )))
         (set assemble-types-section (SCons( (close assemble-types-section) (close(SAtom 'struct\s_s)) )))
         (set assemble-types-section (SCons( (close assemble-types-section) (close(mangle-c-type( base-type body ))) )))
         (set assemble-types-section (SCons( (close assemble-types-section) (close(SAtom '{\n_s)) )))
         (set assemble-types-section (SCons( (close assemble-types-section) (close(SAtom '\tlong\sfield_0\:\n_s)) )))
         (if has-case (
            (set assemble-types-section (SCons( (close assemble-types-section) (close(SAtom '\tunion\s{\n_s)) )))
            (set assemble-types-section (SCons( (close assemble-types-section) (close cases) )))
            (set assemble-types-section (SCons( (close assemble-types-section) (close(SAtom '\t}\:\n_s)) )))
         ) ())
         (set assemble-types-section (SCons( (close assemble-types-section) (close(SAtom '}\:\n_s)) )))
      ))
   ))
) Nil);

compile-c-type-body-of-base-type :=  (: (HashtableEq( 0_u64 0_u64 (as 0_u64 Tuple<String,Tuple<Type,AST>>[]) )) HashtableEq<String,Tuple<Type,AST>>);

compile-c-typedef := λ(: base-type Type)(: body AST). (: (
   (set compile-c-type-body-of-base-type (.bind( compile-c-type-body-of-base-type (.tag base-type) (Tuple( base-type body )) )))
   (if (.is-lm-struct base-type) (
      (if (.is-open base-type) () (
         (compile-c-typedef-concrete( (: None Maybe<TContext>) base-type body ))
      ))
      (match base-type (
         ()
         ( (TGround( tag ts )) (
            (let k (Tuple( tag (.length ts) )))
            (for-each (vt in (.lookup( concrete-type-instances-index k (: LEOF List<Type>) ))) (
               (let tctx (unify( base-type vt )))
               (compile-c-typedef-concrete( tctx vt body ))
            ))
         ))
      ))
   ) ())
) Nil);

compile-c-fields-of-case := λ(: base-type Type)(: tag-definition AST)(: case-number U64). (: (
   (let ft (t1 'Nil_s))
   (match tag-definition (
      ()
      ( (App( (Lit( tag _ )) case-definition )) (
         (set ft (type-of-s-with-fields( base-type tag case-definition case-number )))
      ))
      ( _ () )
   ))
   ft
) Type);

compile-c-casedef := λ(: tctx Maybe<TContext>)(: fields Type)(: case-number U64)(: blame AST). (: (
   (let r (+(
      (SAtom 'struct\s{_s)
      (+(
         (compile-c-casedef-fields( tctx fields case-number 1_u64 blame ))
         (SAtom '}\:\n_s)
      ))
   )))
   r
) S);

compile-c-mark-field-as-seen := λ(: tt Type). (: (
   (if (>( (.arity tt) 0_u64 )) (
      (match tt (
         ()
         ( (TGround( 'Array_s tt-ps )) (
            (for-each (child in tt-ps) (
               (compile-c-mark-field-as-seen child)
            ))
         ))
         ( _ () )
      ))
      (let base-body (.lookup( compile-c-type-body-of-base-type (.tag tt) (Tuple( TAny ASTEOF )) )))
      (let base-type (.first base-body))
      (let body (.second base-body))
      (if (&&( (non-zero body) (not(.lookup( is-cstruct-hard-compiled-index tt 0_u64 ))) )) (
         (if (non-zero(.lookup( index-c-type-ordinal tt '_s ))) () (
            (compile-c-typedef-concrete-ordinal( tt body ))
            (let tctx (unify( base-type tt )))
            (set continue-compile-c-typedefs-concrete (cons(
               (Tuple( (Tuple( tctx tt )) body ))
               continue-compile-c-typedefs-concrete
            )))
            (match tt (
               ()
               ( (TGround( _ tt-ps )) (
                  (for-each (child in tt-ps) (
                     (compile-c-mark-field-as-seen child)
                  ))
               ))
               ( _ () )
            ))
         ))
      ) ())
   ) ())
) Nil);

compile-c-casedef-fields := λ(: tctx Maybe<TContext>)(: fields Type)(: case-number U64)(: field-index U64)(: blame AST). (: (
   (let r SNil)
   (match fields (
      ()
      ( (TGround( 'Nil_s _ )) () )
      ( (TGround( 'Cons_s (LCons( ft (LCons( rst _ )) )) )) (
         (compile-c-mark-field-as-seen(substitute(tctx ft)))
         (let def-type (mangle-c-declaration( ft blame )))
         (set r (+( (+( (.first def-type) (SAtom '\sfield__s) )) (+(
            (+(
               (SAtom(to-string(+( (*( case-number 1000_u64 )) field-index ))))
               (.second def-type)
            ))
            (SAtom '\:_s)
         )))))
         (set r (+( r (compile-c-casedef-fields( tctx rst case-number (+( field-index 1_u64 )) blame )) )))
      ))
      ( ft (
         (compile-c-mark-field-as-seen(substitute(tctx ft)))
         (let def-type (mangle-c-declaration( ft blame )))
         (set r (+( (+( (.first def-type) (SAtom '\sfield__s) )) (+(
            (+(
               (SAtom(to-string(+( (*( case-number 1000_u64 )) field-index ))))
               (.second def-type)
            ))
            (SAtom '\:_s)
         )))))
      ))
   ))
   r
) S);
