
## faster than [type(List<t>)]. should be used when you append or remove data a lot
type Vector<t> = Vector { data: t[], length: U64, capacity: U64 };

let mk-vector(type: Type<t>, capacity: U64): Vector<t> = (
   let data-sz = sizeof(t) * capacity;
   Vector { (malloc(data-sz) as t[]), 0_u64, capacity }
);

## generate a [type(Vector<t>)], in which each element is [value]
let fill-vector(value: t, len: U64): Vector<t> = (
   let v = mk-vector(type(t), len);
   while len > 0 {
      v[len-1] = value;
      len = len - 1;
   };
   v
);

## does not change the length length, or destroy old elements!
let .realloc(v: Vector<t>, target-capacity: U64): Vector<t> = (
   let data-sz = sizeof(t) * target-capacity;
   let newp = realloc(v.data as ?[], data-sz) as t[];
   Vector { newp, v.length, target-capacity }
);

## reserve additional num elements to the current length
let .reserve-additional(v: Vector<t>, additional: U64): Vector<t> = (
   if (v.capacity - v.length) < additional {
      v.realloc(v.length + additional)
   } else {
      v
   }
);

## INPUT VECTOR GETS POISONED
let .push(v: Vector<t>, i: t): Vector<t> = (
   if v.length >= v.capacity {
      let new-cap = if v.capacity == 0 {
         4_u64
      } else { # mul 1.5
         (v.length >> 1_u64) + v.length
      };
      v = v.realloc(new-cap);
   };
   v.data[v.length] = i;
   v.length = v.length + 1_u64;
   v
);

## shrinks the vector if it has way too many elements. shouldnt be called manually
let .shrink(v: Vector<t>): Vector<t> = (
    let too-much = v.capacity - v.length;
    let minimum = (v.length >> 1) + v.length; # mul 1.5
    if too-much > minimum {
       v = v.realloc(minimum);
    };
    v
);

## INPUT VECTOR GETS POISONED
let .remove-front(v: Vector<t>, num: U64): Vector<t> = (
    if num > v.length() {
        fail("tried to remove \{num} elements from vector, but only have \{v.length()}");
    };

    let i = num;
    while i < v.length() {
        v[i-num] = v[i];
        i = i + 1;
    };
    v.length = v.length - num;

    v.shrink();
);

## INPUT VECTOR GETS POISONED
let .remove-back(v: Vector<t>, num: U64): Vector<t> = (
    if num > v.length() {
        fail("tried to remove \{num} elements from vector, but only have \{v.length()}");
    };

    v.length = v.length - num;

    v.shrink();
);

## INPUT VECTOR GETS POISONED
let .pop(v: Vector<t>): Tuple<Vector<t>, t> = (
    if v.length() == 0 {
        fail("Tried to pop from empty Vector.");
    };

    let lasti = v.length - 1;
    let last = v[lasti];
    v = v.remove-back(1);

    Tuple { v, last }
);

let $"[]"( v: Vector<t>, i: U64 ): t = (
   if i >= v.length {
      fail("Vector Index Out of Bounds");
   };
   v.data[i]
);

let $"+"( vl: Vector<t>, vr: Vector<t> ): t = (
   let v = mk-vector(type(t), vl.length + vr.length);
   let vi = 0_u64;
   while vi < vl.length { v = v.push(vl[vi]); };
   vi = 0_u64;
   while vi < vr.length { v = v.push(vr[vi]); };
   v
);

let $"set[]"( v: Vector<t>, i: U64, val: t ): Nil = (
   if i >= v.length {
      fail("Vector Index Out of Bounds");
   };
   v.data[i] = val; 
   ()
);

let .reverse-self(v: Vector<t>): Nil = (
   let i = 0_u64;
   while i < (v.length >> 1) {
      let ri = v.length - i - 1;
      let temp = v[i];
      v[i] = v[ri];
      v[ri] = temp;
      i = i + 1;
   };
);

let .sort(v: Vector<t>): Vector<t> = (
   let n = v.length;
   let i = 0_u64;
   while i < n - 1_u64 {
      let swapped = 0;
      let j = 0;
      while j < n - i - 1_u64 {
         if v[j + 1_u64] < v[j] {
            let tmp = v[j];
            v[j] = v[j + 1_u64];
            v[j + 1_u64] = tmp;
            swapped = 1;
         };
         j = j + 1_u64;
      };
      i = i + 1_u64;
      if not(swapped) {
         i = n;
      };
   };
   v
);

let print(v: Vector<x>): Nil = (
   let vi = 0_u64;
   print(c"[");
   while vi < v.length {
      if vi > 0_u64 then print(c",");
      print(v.data[vi]);
      vi = vi + 1_u64;
   };
   print(c"]");
);

interface FromVector<t> {
   let .move-to(self: Vector<t>, out: Type<FromVector<t>>): FromVector<t>;
};

type CString => FromVector<U8>;

## MAKES INPUT VECTOR INVALID
let .move-to(self: Vector<U8>, res: Type<CString>): CString = (
   self = self.push(0_u8);
   self = self.shrink();
   (self.data as U8[]) as CString
);

type SmartString => FromVector<U8>;

## MAKES INPUT VECTOR INVALID
let .move-to(self: Vector<U8>, res: Type<SmartString>): SmartString = (
   intern(self.move-to(type(CString)))
);
